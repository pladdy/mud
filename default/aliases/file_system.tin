#alias read_tin_files {
  #script {init_list} {
    find -E ./%1 -wholename '*.tin' -a ! -regex '.*/init.tin'
  };

  #foreach { $init_list[%*] } {file} {
    #if {@length{$file} > 0} {
      #showme reading $file;
      #read $file;
    };
  };

  #unvar init_list;
}

#alias fshow {
  #if {"%1" == ""} {
    #showme <170>Need to provide something to search for.;
    #return;
  };

  #script {fshow_files} {
    grep -rliI --exclude '*map.txt' --exclude '*.git*' '%0' ./;
  };

  #foreach { $fshow_files[%*] } {file} {
    #if {"$file" != ""} {
      #system { cat '$file' };
      #showme <170>From file: $file\n;
    }
  }
}

#alias grep {
  #if {"%1" != ""} {
    #system grep -r '%1' ./%2;
  }
}

#alias show {
  #if {"%1" == ""} {
    #system { ls -al };
  };
  #else {
    #system {
      if [ -f %1 ] ; then cat %1 ; else ls -al %1 ; fi;
    }
  };
}

#alias lists { show lists/%1 }
#alias notes { show notes/%1 }
