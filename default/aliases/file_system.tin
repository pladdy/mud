#alias read_tin_files {
  #script {init_list} {
    find ./%1 -wholename '*.tin' | grep -v init
  };

  #foreach { $init_list[%*] } {file} {
    #if {@length{$file} > 0} {
      #showme reading $file;
      #read $file;
    };
  };

  #unvar init_list;
}

#alias fsearch {
  #if {"%1" == ""} {
    #showme <170>Need to provide something to search for.;
    #return;
  };

  #script {fshow_files} {
    grep -rliI --exclude '*map.txt' --exclude '*.git*' '%0' ./;
  };

  #foreach { $fshow_files[%*] } {file} {
    #if {"$file" != ""} {
      #system { cat '$file' };
      #showme <170>From file: $file\n;
    }
  }
}

#alias grep {
  #if {"%1" != ""} {
    #system grep -r '%1' ./%2;
  }
}

#alias fshow {
  #if {"%1" == ""} {
    #system { ls -al };
  };
  #else {
    #system {
      first_file_found=$(find ./ -wholename '*%1*' | head -n 1);

      if [ -n "${first_file_found}" ]; then
        msg="first file found: ${first_file_found}";
        printf "\e[97m$msg\e[0m\n";
        cat ${first_file_found};
      elif [ -f %1 ]; then
        cat %1;
      else
        ls -al %1;
      fi;
    }
  };
}

#alias lists { show lists/%1 }
#alias notes { show notes/%1 }
