#nop Grep the map for text;

#alias gmap {
  #if {@length{%0} > 0} {
    #system grep "%0" $map_path;
  } {
    #showme Need some arguments to grep for;
  }
}

#nop Peek in a room to see it's data;

#alias pmap {
  #switch {"%1"} {
    #case "n" #var {opposite_dir} {s};
    #case "s" #var {opposite_dir} {n};
    #case "w" #var {opposite_dir} {e};
    #case "e" #var {opposite_dir} {w};
    #case "nw" #var {opposite_dir} {se};
    #case "se" #var {opposite_dir} {nw};
    #case "ne" #var {opposite_dir} {sw};
    #case "sw" #var {opposite_dir} {ne};
  }
  #map move %1;
  #map info;
  #map move $opposite_dir;
  #unvar opposite_dir;
}

#alias map { #map map; }

#alias map_run {
  #format {run_to} {%l} {%1};
  #map run $run_to;
  #unvar run_to;
}

#alias run {map_run %0}

#alias map_hide { #unsplit; }

#alias map_load {
  #map read $map_path;
  #map goto home;
  #showme Map loaded, set to home.;
}

#alias map_info { #map info; }

#alias map_lock {
  #showme Map is Read only.;
  #map flag static on;
  #alias map_undo {#nop;};
  !brief on yes;
  #unsplit;
}

#nop TODO:;
#nop fix this ugliness of handling globals in a file with no keys...;

#alias map_refresh {
  #nop Try to read mud_map_height from env or set to a default;
  #script {mud_map_width} {
    grep 'width'  $map_dir/map_globals.txt | sed 's/width://'
  };
  #if {! $mud_map_width[1] > 0} {#var {mud_map_width[1]} 100};

  #script {mud_map_height} {
    grep 'height'  $map_dir/map_globals.txt | sed 's/height://'
  };
  #if {! $mud_map_height[1] > 0} {#var {mud_map_height[1]} 58};

  #format {map_dimensions} {%sx%s} {$mud_map_width[1]} {$mud_map_height[1]};
  #map map $map_dimensions $map_dir/current_map.txt;
}

#alias mapr { map_refresh }

#alias map_save {
  #map write $map_path;
  #showme Map saved to $map_path;
}

#alias map_show {
  #split 16 1;
  #map flag vtmap on;
}

#alias map_write {
  #showme Map is writable.;
  #map flag static off;
  #alias map_undo { #map undo; };
  #nop !brief on no;
  #nop map_show;
}

#alias map_undo {#nop;}

#nop 'Private' aliases used internally;

#alias chaos_area_port {
  #showme porting to $chaos_port_location!;
  #map goto $chaos_port_location;
  #var chaos_port_location {};
}

