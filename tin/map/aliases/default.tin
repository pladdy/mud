#alias chaos_area_port {
  #showme <170>Porting to $chaos_port_location!;
  #map goto $chaos_port_location;
  #var chaos_port_location {};
}

#alias nn { #10 n; }
#alias ww { #10 w; }
#alias ee { #10 e; }
#alias ss { #10 s; }
#alias nne { #10 ne; }
#alias nnw { #10 nw; }
#alias sse { #10 se; }
#alias ssw { #10 sw; }

#alias map_load {
  #map read $map_path;
  #map goto home;
  #showme Map loaded, set to home.;
}

#nop The map showing up permanently and not scrolling away depends on 'split';
#nop The status_bar alias is responsible for splitting the screens;
#alias map_refresh {
  #local SPLIT_ADJUSTMENT 4;
  #local WIDTH_OFFSET 101;

  #screen get rows screen_height;
  #screen get cols screen_wdith;

  #math map_height {$screen_height - $SPLIT_ADJUSTMENT};
  #math map_width {$screen_width - $WIDTH_OFFSET};

  #math box_height {$map_height + 2};
  #var box_width $screen_width;

  debug map dimensions: $map_height $map_width;
  #nop #map map $map_height $map_width draw 1 $WIDTH_OFFSET $box_height $box_width {<170>Map Yo!};

  #map map $map_height $map_width variable current_map;
  #replace {current_map} {\\} {\\};

  #map get all map[room_info];
  debug draw box: 1 $WIDTH_OFFSET $box_height $box_width;
  #draw jade box 1 $WIDTH_OFFSET $box_height $box_width {$current_map} {
    <170>Room Name: $map[room_info][name]\n<170>Room Note: $map[room_info][note]
  };
}

#alias mapr { map_refresh }

#nop When 'running' disable refresh, it makes the 'run' much faster;

#alias map_run {
  #if {"$stepper" == "on"} {stepper off};

  #format {run_to} {%l} {%1};
  #unevent {MAP ENTER ROOM};

  #if {"$tracker" == "on"} {
    !ansi off;
    #list seen_monsters clear;
  };

  #map run $run_to;
  refresh_map_on_enter;
  map_refresh;
  #unvar run_to;

  #if {"$tracker" == "on"} {
    !ansi on;
    !glance;
  };
}

#nop Peek in a room to see it's data;

#alias pmap {
  #switch {"%1"} {
    #case "n" {#map jump 0 1; #map info; #map jump 0 -1;};
    #case "s" {#map jump 0 -1; #map info; #map jump 0 1;};
    #case "w" {#map jump -1 0; #map info; #map jump 1 0;};
    #case "e" {#map jump 1 0; #map info; #map jump -1 0;};
    #case "nw" {#map jump -1 1; #map info; #map jump 1 -1;};
    #case "se" {#map jump 1 -1; #map info; #map jump -1 1;};
    #case "ne" {#map jump 1 1; #map info; #map jump -1 -1;};
    #case "sw" {#map jump -1 -1; #map info; #map jump 1 1;};
  }
}

#alias refresh_map_on_enter {
  #nop See events.tin for this alias;
  set_map_enter_room_event;
}

#alias run {map_run %0}
