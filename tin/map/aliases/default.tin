#alias chaos_area_port {
  #showme <170>Porting to $chaos_port_location!;
  #map goto $chaos_port_location;
  #var chaos_port_location {};
}

#alias nn { #10 n; }
#alias ww { #10 w; }
#alias ee { #10 e; }
#alias ss { #10 s; }
#alias nne { #10 ne; }
#alias nnw { #10 nw; }
#alias sse { #10 se; }
#alias ssw { #10 sw; }

#alias map_load {
  #map read $map_path;
  #map goto home;
  #showme Map loaded, set to home.;
}

#alias map_refresh {
  #local MAP_FILE $map_dir/current_map.txt;

  #nop height/width are unused currently but might figure out how to use...;
  #format map_height_fraction {%R};
  #math {map_height} {$map_height_fraction / .9};
  #format {map_height} {%d} {$map_height};

  #format map_width_fraction {%C};
  #math {map_width} {$map_width_fraction / .4 * .5};
  #format {map_width} {%d} {$map_width};

  #map map 0 0 variable current_map;
  #system { echo "$current_map" > $MAP_FILE };
}

#alias mapr { map_refresh }

#nop When 'running' disable refresh, it makes the 'run' much faster;

#alias map_run {
  #format {run_to} {%l} {%1};
  #unevent {MAP ENTER ROOM};

  #if {"$tracker" == "on"} {
    !ansi off;
    #list seen_monsters clear;
  };

  #map run $run_to;
  refresh_map_on_enter;
  map_refresh;
  #unvar run_to;

  #if {"$tracker" == "on"} {
    !ansi on;
    !glance;
  };
}

#alias map_show {
  #split 16 1;
  #map flag vtmap on;
}


#nop Peek in a room to see it's data;

#alias pmap {
  #switch {"%1"} {
    #case "n" {#map jump 0 1; #map info; #map jump 0 -1;};
    #case "s" {#map jump 0 -1; #map info; #map jump 0 1;};
    #case "w" {#map jump -1 0; #map info; #map jump 1 0;};
    #case "e" {#map jump 1 0; #map info; #map jump -1 0;};
    #case "nw" {#map jump -1 1; #map info; #map jump 1 -1;};
    #case "se" {#map jump 1 -1; #map info; #map jump -1 1;};
    #case "ne" {#map jump 1 1; #map info; #map jump -1 -1;};
    #case "sw" {#map jump -1 -1; #map info; #map jump 1 1;};
  }
}

#alias run {map_run %0}

#alias refresh_map_on_enter {
  #event {MAP ENTER ROOM} {
    #delay {0} {map_refresh};
  }
}
