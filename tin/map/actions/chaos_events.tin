#nop Atlantis;

#action {^The airlock door swings open, you quickly step out.$} {
  #if {$map[current_room] == 9146 && $map[last_room] == 9145} {
    #map goto 9147;
  } {
    #map goto 9145;
  }
}

#nop  Ashridge Acres;

#action {^AREA NAME: Ashridge Acres [Urian]$} {
  #map goto 35416;
}

#nop Chaotic Forest;

#action {^You let yourself go and feel the mist pull you into another reality.} {
  #map goto 35449;
}

#nop Chaotic sewers (from mouse trails);

#action {^As you fly through the air, your body reassumes its normal shape, no$} {
  #if {$map[current_room] == 11729} {
    #map goto 23057;
  }
}

#nop Book nook;

#nop Red Barcheta;

#action {^You scream for joy as you leap from the train at the border!$} {
  #if {$map[current_room] == 21389} {#map goto 21390;}
}

#nop Mortal Kombat cd;

#action {^You can almost see the swirling gates between the Earth and the} {
  #if {$map[current_room] == 11990} {#map goto 11991;}
}

#nop Death taco;

#action {^O SNAP!!! Shredded Lettuce grabs you and THROWS you off the slide!$} {
  #if {$map[current_room] == 12484} {#map goto 12492;}
}

#action {^The taco grabs you and THROWS you out of the room!$} {
  #if {$map[current_room] == 12491} {
    #delay 2 {
      #show me <fca>You got thrown from room: figure out where you are and come back.;
    }
  }
}

#action {^You try to grab on to the electrical cord to get out of here but a roach$} {
  #map goto 12492;
}

#nop  if flying, lettuce special moves you 1 east;
#action {^You manage to glide to a graceful stop in midair and return to the slide tray.} {
  #map goto 12485;
}

#nop  if flying, death taco special moves you 1 east;
#action {^You get KNOCKED back hard, but then are able to make yourself glide to a graceful stop in midair.} {
  #map goto 12490;
}

#nop Erratio forest

#action {^You squeeze your way into the Erratio forest.$} {
  #map goto 24412;
}

#nop Kyoto;

#alias kyoto_dirs {
  log.debug 'kyoto_dirs' called with %1;

  #switch {"%1"} {
    #case "face_west" {
      log.info Facing west in Kyoto.;
      #alias w {#send forward; #map move w};
      #alias n {#send right; #map move n; kyoto_dirs face_north};
      #alias s {#send left; #map move s; kyoto_dirs face_south};
      #alias e {#send backward; #map move e; kyoto_dirs face_east};
    };
    #case "face_east" {
      log.info Facing east in Kyoto.;
      #alias w {#send backward; #map move w; kyoto_dirs face_west};
      #alias n {#send left; #map move n; kyoto_dirs face_north};
      #alias s {#send right; #map move s; kyoto_dirs face_south};
      #alias e {#send forward; #map move e};
    };
    #case "face_north" {
      log.info Facing north in Kyoto.;
      #alias w {#send left; #map move w; kyoto_dirs face_west};
      #alias n {#send forward; #map move n};
      #alias s {#send backward; #map move s; kyoto_dirs face_south};
      #alias e {#send right; #map move e; kyoto_dirs face_east};
    };
    #case "face_south" {
      log.info Facing south in Kyoto.;
      #alias w {#send right; #map move w; kyoto_dirs face_west};
      #alias n {#send backward; #map move n; kyoto_dirs face_north};
      #alias s {#send forward; #map move s};
      #alias e {#send left; #map move e; kyoto_dirs face_east};
    };
    #default {
      log.info Removing kyoto direction aliases.;
      #unalias w;
      #unalias n;
      #unalias s;
      #unalias e;
    }
  }
}

#action {^A Poorly-lit Street} {
  #nop #map goto 32481;
  #nop kyoto_dirs face_west;
}

#nop Midgard;

#action {^As you read the runes, you being to feel yourself being pulled$} {
  #var chaos_port_location {$map[current_room]};
  #map goto midgard;
}

#nop Silver/Blue;

#action {^A soft jerk, the balloon rises slowly, but the landscape below fades$} {
  #switch { $map[current_room] } {
    #case {12868} {#map goto 12869;};
    #case {12869} {#map goto 12868;};
    #case {32946} {#map goto 32947;};
    #case {32947} {#map goto 32946;};
  };
}

#nop Tree of life;

#action {^%!{[> ]*}The light bends into the shape of a doorway.  Through it} {
  #map goto 30036;
}

#nop Wandering Refridgerator;

#action {^You open the huge refrigerator and step inside.$} {
  #map goto 20497;
}

#nop R'lyeh;

#action {^a stream! The sea is gone, you can breath again! But where are you?$} {
  #if {$map[current_room] == 21786} {#map go 11692}
}

#nop The Zodiac;

#action {^Cool!  It has your sign!  Today is a %1....$} {
  #map goto 22124;
}

#nop Chaos TV: The simpsons;

#action {^The Springfield Comments Board} {
  #if {$map[current_room] != 22261
       && $map[current_room] != 22266
       && $map[current_room] != 22219} {
    #map goto 22218;
  };
}

#nop Chaos TV: dexter's lab;

#action {^AREA NAME: Dexter's Laboratory \[Adalius\]$} {
  #map goto 25048;
}

#nop Chaos TV: dragonball z;

#action {^AREA NAME: Dragonball Z \[Aneki\]$} {
  #map goto 35693;
}

#nop Cowville;

#action {^The boat pulls up at the Isle of Cows.$} {
  #map goto 35563;
}

#action {^The boat pulls up at the Cowville Wharf.$} {
  #map goto 30150;
}

#nop Ice Cream Truck;

#action {^%!{[> ]*}You enter the truck with one thing on your mind... ICE CREAM!} {
  #map goto 29955;
}

#nop Illumanit Pyramid;

#action {^Pyramid Entrance (n,out)} {
  #map goto 25090;
}

#nop Myth Area;

#action {^When you manage to open them, your surroundings have changed completely!$} {
  #var chaos_port_location {$map[current_room]};
  #map goto myth_area;
  #map link leave $chaos_port_location;
  #var chaos_port_location {};
}

#nop WWF;

#action {^You ring the bell, but nothing happens.  You've won!} {
  #map goto 25903;
}

#nop  Witches hat;
#action {^Frayed Reality} {
  #map goto 34091;
}

#action {^AREA NAME: Witch's Hat [Canislupus]$} {
  #map goto 34091;
}

#action {^<-> The Transporter Pod <-> (out)$} {
  #if {$map[current_room] == 34383} {
    #map goto 34382;
 }
}


#nop  Dundees;
#action {^The foliage in this part of the swamp is extremely thick and will} {
  #map undo;
  #if {"$stepper" == "on"} {take_a_step};
}