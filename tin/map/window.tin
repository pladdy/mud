#nop The window showing up permanently and not scrolling away depends on 'split';
#nop The status_bar alias is responsible for splitting the screens;
#alias map_refresh {
  #local SPLIT_ADJUSTMENT 4;
  #nop  The below offset is 1 character more than the text window;
  #local WIDTH_OFFSET 101;

  #screen get rows screen_height;
  #screen get cols screen_width;

  #math map_height {($screen_height - $SPLIT_ADJUSTMENT) / 2};
  #math map_width {$screen_width - $WIDTH_OFFSET};

  #math map_box_height {$map_height + 2};
  #var map_box_width $screen_width;

  log.debug map dimensions, height: $map_height width: $map_width;

  #map map $map_height $map_width variable current_map;
  #map get all map[room_info];

  log.debug draw box: 1 $WIDTH_OFFSET $map_box_height $map_box_width;
  #draw jade box 1 $WIDTH_OFFSET $map_box_height $map_box_width {$current_map} {
    <170> Room Area: $map[room_info][area]\n
    <170>Room Desc: $map[room_info][data]\n
    <170>Room Name: $map[room_info][name]\n
    <170>Room Note: $map[room_info][note]
  };
}

#alias mapr { map_refresh }
