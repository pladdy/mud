#function first_letter {
  #regexp {%0} {%.} {#return &1};
}

#function length {
  #var result 0;

  #parse {%0} {char} {
    #math {result} { $result + 1 }
  };

  #return $result;
}

#function join {
  #list join_list create %0;

  #var result {};

  #foreach { $join_list[%*] } {arg} {
    #regexp {$arg} {^%s$} {#var check 1} {#var check 0};
    #if {$check == 1} {#continue};

    #regexp $result {^$} {#var check 1} {#var check 0};

    #if {$check == 1} {
      #var result {$arg};
    } {
      #var result {$result $arg};
    };
  };

  #unvar join_list;
  #unvar arg;
  #return $result;
}

#nop  Estimate how many rounds of combat are left;
#nop  Expects var rounds_in_combat to be tracked by guild implementation;
#nop  Expects var enemy to be an int or float;
#function combat_rounds_left {
  #var result {};

  #if {$rounds_in_combat > 0} {
    #math {damage_left} {$enemy};
    #math {damage_so_far} {100.0 - $enemy};
    #math {average_dmg} {$damage_so_far / $rounds_in_combat};

    #if {$average_dmg > 0} {
      #math {rounds_left} {$damage_left / $average_dmg}
    };
    #var result $rounds_left;
  };
  #else {#var result 0};

  #return $result;
}
