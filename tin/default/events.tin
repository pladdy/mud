#nop MAP ENTER ROOM is set with an alias so it can be toggled;
#nop see tin/map/aliases/default.tin;

#event {MAP EXIT ROOM} {
  #var map[current_room] %1;
  #var map[last_room] %0;
}

#nop Logging;

#var {session_log} {./3k_session.log};
#event {SESSION CONNECTED} {#log o {$session_log}}

#nop Define alias here to make available to events below;

#alias export_session {
  #system {
    echo \#var {map[current_room]} {$map[current_room]} > last_session.tin;
    echo \#var {chaos_port_location} {$chaos_port_location} >> last_session.tin;
    echo \#var {wielded_weapon} {$wielded_weapon} >> last_session.tin;
  };
}

#event {SESSION DISCONNECTED} {
  #showme <170>Session disconnecting...;
  map_save;
  export_session;

  #if {"$quit" == "true"} {#unvar flying};
  #else {#system {tail -n 20 $session_log}};
}

#nop when you type '#end' this event is fired;

#event {PROGRAM TERMINATION} {
  #showme <170>Program shutting down...;
  #system {
    tmux ls > /dev/null 2>&1;
    if [ $? -eq 0 ]; then
      tmux kill-session -t tintin;
    fi;
  };
}
