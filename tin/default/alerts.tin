#var alerts[says] None;
#var alerts[tells] None;

#nop create tell blocklist;
#list tells_blocklist clear;

#nop knights NPCs;
#if {"$guild" == "knights"} {
  #list tells_blocklist add {arvin};
  #list tells_blocklist add {dame camilla bedwere};
  #list tells_blocklist add {dame zara diones};
  #list tells_blocklist add {gabriella};
  #list tells_blocklist add {klvorn};
  #list tells_blocklist add {mia jas};
  #list tells_blocklist add {vokorin};
  #list tells_blocklist add {sir arruns thelton};
  #list tells_blocklist add {sir elgar rayne};
  #list tells_blocklist add {sir fors belerent};
  #list tells_blocklist add {sir rea};
  #list tells_blocklist add {tauron};
};

#nop comment out/nop character for testing;
#nop #list tells_blocklist add {$character};
#list tells_blocklist add {antonio};
#list tells_blocklist add {robert};
#list tells_blocklist add {the vampire};
#list tells_blocklist simplify;

#function format_says {
  #var result {\n<170>Says:};

  #foreach {$alerts[says]} {target} {
    #if {"$target" != "None" && $target[count] > 0} {
      #var result {$result\n$target[name] ($target[count]): $target[last]};
    };
  };
}

#function format_tells {
  #var result {\n<170>Tells:};

  #foreach {$alerts[tells]} {target} {
    #if {"$target" != "None" && $target[count] > 0} {
      #var result {$result\n$target[name] ($target[count]): $target[last]};
    };
  };
}

#action {^%!{[> ]*}%1 says: %2} {
  #var alerts[says][%1][name] %1;
  #var alerts[says][%1][last] %2;
  #math alerts[says][%1][count] {$alerts[says][%1][count] + 1};

  alerts_refresh;
  #delay {alerts_says} {
    #var alerts[says] None;
    alerts_refresh;
  } {5};
}

#action {^%!{[> ]*}%1 tells you: %2} {
  #var send_alert {true};

  #foreach $tells_blocklist blocked {
    #regexp {%1} {%i$blocked} {#var send_alert false; #break};
  }

  #if {"$send_alert" == "true"} {
    #var alerts[tells][%1][name] %1;
    #var alerts[tells][%1][last] %2;
    #math alerts[tells][%1][count] {$alerts[tells][%1][count] + 1};

    alerts_refresh;
    sound_beeps;
    set_repeating_alarm;

    #if {"$stepper" == "on"} {
      stepper off;
    }
  }
}

#nop The window showing up permanently and not scrolling away depends on 'split';
#nop The status_bar alias is responsible for splitting the screens;
#alias alerts_refresh {
  #local SPLIT_ADJUSTMENT 4;
  #local WIDTH_OFFSET 101;

  #screen get rows screen_height;
  #screen get cols screen_wdith;

  #math alert_height {($screen_height - $SPLIT_ADJUSTMENT) / 3};
  #math alert_height {$alert_height * 2};
  #math alert_width {$screen_width - $WIDTH_OFFSET};

  #math alert_box_height {$alert_height + 2};
  #math alert_box_width {$screen_width};

  debug alert dimensions, height: $alert_height width: $alert_width;
  debug draw jade box $alert_box_height $WIDTH_OFFSET $screen_height $alert_box_width;

  #math brh {$screen_height - 2};
  #math brw {$alert_box_width};
  #local formatted_says @format_says{};
  #local formatted_tells @format_tells{};

  #draw jade box $alert_box_height $WIDTH_OFFSET $brh $brw {Alerts} {
    $formatted_says
    \n\n
    $formatted_tells
  };
}

#alias alertsr { alerts_refresh }

#alias cth {
  #send ctellhist;
}

#alias sh {
  #send sayhist;
  #delay 1 {#var alerts[says] None; alerts_refresh;};
}

#alias set_repeating_alarm {
  #delay {repeating_alarm} {
    log.warn You have tells, 'tellhist' or 'th' to review them.;
    sound_beeps;
    set_repeating_alarm;
  } {60};
}

#alias th {
  #send tellhist;
  #delay 1 {#var alerts[tells] None; alerts_refresh;};
  #undelay {repeating_alarm};
}

#alias hmh {
  #send hmhist;
}

#alias sound_beeps {
  #system {
    for i in 1 2; do printf '\a'; done
  }
}
