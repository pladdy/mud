#nop  I set this up this way because I tried using an alias, but the '%0'
#nop  positional args in the actions wouldn't work when nested in an alias.

#class {chats} {kill};
#class {chats} {open};


#alias build_says_blocklist {
  build_default_says_blocklist;
  simplify_says_blocklist
}

#alias build_tells_blocklist {
  build_default_tells_blocklist;
  simplify_tells_blocklist
}

#alias build_default_says_blocklist {
  #script {says_blocklist} {cat tin/default/chats/say_blocklist.txt | grep -v '#'};
}

#alias build_default_tells_blocklist {
  #script {tells_blocklist} {cat tin/default/chats/tell_blocklist.txt | grep -v '#'};
}

#alias simplify_says_blocklist {
  #list says_blocklist simplify;
}

#alias simplify_tells_blocklist {
  #list tells_blocklist simplify;
}

#alias -catch_overflow {
  #read tin/default/chats/overflow.tin;
}

#action {^%!{[> ]*}[PARTY] {[A-Za-z]+}: %2} {
  #list chats add {%0};
  chats_refresh;
  -catch_overflow;
};

#action {^%!{[> ]*}{[A-Za-z ]+} says: %2} {
  #var show_chat true;

  log.debug 'say' matched: matched=%0, actor=%1;

  #foreach $says_blocklist blocked {
    log.debug checking blocklist, blocked=$blocked, actor=%1;
    #regexp {%1} {%i{$blocked}} {#var show_chat false};
  };

  #if {"$show_chat" == "true"} {
    #list chats add {%0};
    chats_refresh;
    -catch_overflow;
  }
};

#alias _handle_tells {
  #if {$disable_chat_alerts != 1 && "$mip[enabled]" != "true"} {
    #list chats add {%0};
    chats_refresh;
    -catch_overflow;

    sound_beeps;
    set_repeating_alarm;
    #if {"$stepper" == "on"} {stepper off};
    #undelay reactivate_stepper;
  };
}

#nop  Link tells;
#action {^%!{[> ]*}{[A-Za-z ]+} LTs (%2)} {
  #var send_alert true;

  #foreach $tells_blocklist blocked {
    log.debug checking blocklist, blocked=$blocked, actor=%1;
    #regexp {%1} {%i{$blocked}} {#var send_alert false};
  };

  #if {"$send_alert" == "true"} {
    log.debug 'tell' matched: matched=%0, actor=%1;
    _handle_tells %0;
  };
};

#nop  Tells;
#action {^%!{[> ]*}{[A-Za-z ]+} tells you: %2} {
  #var send_alert true;

  #foreach $tells_blocklist blocked {
    log.debug checking blocklist, blocked=$blocked, actor=%1;
    #regexp {%1} {%i$blocked} {#var send_alert false};
  };

  #if {"$send_alert" == "true"} {
    log.debug 'tell' matched: matched=%0, actor=%1;
    _handle_tells %0;
  };
};

#nop  Create chat blocklists;
#list tells_blocklist clear;
build_tells_blocklist;

#list says_blocklist clear;
build_says_blocklist;

#var disable_chat_alerts 0;

#class {chats} {close};
