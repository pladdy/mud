#alias aliases {
  #var sed_str {s/#alias[[:space:]]*([a-zA-Z0-9_]+)[[:space:]].+/\1/p};

  #if {"%1" == "guild"} {
    #showme <170>Guild aliases:;
    print_system_output egrep -r '#alias' tin/guilds/$guild | sed -En '$sed_str';
  };
  #elseif {"%1" == "default"} {
    #showme <170>Default aliases:;
    print_system_output egrep -r '#alias' tin/default | sed -En '$sed_str';
  };
  #else {
    #showme Usage: 'aliases guild|default';
  }

  #unvar sed_str;
}

#alias auction {
  #if {@length{%3} == 0} {
    #send !auction %0;
  } {
    #send !auction %1;
    #send %2;
    #send %3;
    #send %4;
  }
}

#alias bootrun {fcat notes/bootrun.txt}

#alias f12 {
  #loop 2 12 i {#send !sell all from bag $i};
}

#nop HighMortal powers;

#alias hms {
  #send !hmsever;
}

#alias hmb {
  #send !hmblast %0;
}

#alias kk {
  #if {@length{%0} == 0} {
    #showme need a target;
  } {
    #var kk_list {%0};
    #replace {kk_list} {,} {;};

    #foreach {$kk_list} {kk} {#send !k $kk};

    #nop #unvar kk_list;
  }
}

#alias l {
  #if {@length{%1} > 0} {#send !look at %0};
  #else                 {#send !look};
}

#alias ls {
  #if {"%0" == ""} {
    #send !look;
    #send !search;
  };
  #else {
    #send !look at %0;
    #send !search %0;
  }
}

#nop Print the output of given script command;

#alias print_system_output {
  debug Output from '%0';
  #script {content} {%0};
  #foreach { $content[%*] } {line} {#showme $line};
  #unvar content;
}

#alias quit {
  #send !quit;
  #end;
}

#alias read_tin_files {
  #script {tin_list} {
    find ./%1 -wholename '*.tin' | grep -v '.bootstrap.tin';
  };

  #foreach { $tin_list[%*] } {file} {
    #if {@length{$file} > 0} {
      #showme reading $file;
      #read $file;
    };
  };

  #unvar tin_list;
}

#alias reload_map {
  #showme Setting up map class;
  #class map kill;
  #class map open;
  read_tin_files $map_dir;
  #class map close;
}

#alias reload_tin {
  #read tin/default/.bootstrap.tin;
  reload_map;
}

#alias wemud {
  #system {open https://wemudtogether.com/};
}

#alias wield {
  #var {wielded_weapon} {%0};
  #send !wield %0;
}

#alias wiki {
  #system {open http://wiki.maybeits.us/index.php/};
}
