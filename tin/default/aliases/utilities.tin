#alias aliases {
  #var sed_str {s/#alias[[:space:]]*([a-zA-Z0-9_]+)[[:space:]].+/\1/p};

  #if {"%1" == "guild"} {
    #showme <170>Guild aliases:;
    print_system_output egrep -r '#alias' tin/guilds/$guild | sed -En '$sed_str';
  };
  #elseif {"%1" == "default"} {
    #showme <170>Default aliases:;
    print_system_output egrep -r '#alias' tin/default | sed -En '$sed_str';
  };
  #else {
    #showme Usage: 'aliases guild|default';
  };

  #unvar sed_str;
}

#alias auction {
  #if {@length{%3} == 0} {
    #send !auction %0;
  } {
    #send !auction %1;
    #send %2;
    #send %3;
    #send %4;
  }
}

#alias bootrun {fcat notes/bootrun.txt}

#alias cofs {#send !cofscore};

#alias f12 {
  #loop 2 $BAGS_TO_USE i {#send !sell all from bag $i};
}

#alias forums {
  #system {open https://forums.3k.org/};
}

#nop HighMortal powers;

#alias hms {
  #send !hmsever;
}

#alias hmb {
  #send !hmblast %0;
}

#alias ka {
  #loop 1 %2 loop {#send !kill %1 $loop;};
}

#alias kk {
  #if {@length{%0} == 0} {
    #showme need a target;
  };
  #else {
    #var kk_list {%0};
    #replace {kk_list} {,} {;};
    #foreach {$kk_list} {kk} {#send !k $kk};
  };
}

#alias l {
  #if {@length{%1} > 0} {#send !look at %0};
  #else                 {#send !look};
}

#alias ls {
  #if {"%0" == ""} {
    #send !look;
    #send !search;
  };
  #else {
    #send !look at %0;
    #send !search %0;
  }
}

#alias load_guild_class {
  #showme Setting up %1 class;
  #class $guild kill;
  #class $guild open;
  read_tin_files tin/guilds/%1;
  #class $guild close;
}

#alias load_notes_class {
  #class notes kill;
  #class notes open;
  read_tin_files tin/notes;
  #class notes close;
}

#alias load_mip_class {
  read_tin_files tin/mip;
}

#alias load_stepper_class {
  #class stepper kill;
  #class stepper open;
  read_tin_files tin/stepper;
  #class stepper close;
}

#nop Print the output of given script command;

#alias print_system_output {
  log.debug Output from '%0';
  #script {content} {%0};
  #foreach { $content[%*] } {line} {#showme $line};
  #unvar content;
}

#alias quit {
  #send !quit;
  #end;
}

#alias reload_map {
  #showme Setting up map class;
  #class map kill;
  #class map open;
  read_tin_files $map_dir;
  #class map close;
}

#alias reload_tin {
  #read tin/default/.bootstrap.tin;
  reload_map;
}

#alias read_tin_files {
  #script {tin_list} {
    find ./%1 -wholename '*.tin' | grep -v '.bootstrap.tin';
  };

  #foreach { $tin_list[%*] } {file} {
    #if {@length{$file} > 0} {
      #showme reading $file;
      #read $file;
    };
  };

  #unvar tin_list;
}

#alias sound_beep {#bell}

#alias sound_beeps {
  sound_beep;
  #delay 1 sound_beep;
}

#alias weather {
  #script {my_weather} {curl -s wttr.in/$WEATHER_LOCATION\?format=4};
  #showme $my_weather[1];
}

#alias wemud {
  #system {open https://wemudtogether.com/};
}

#alias wield {
  #var {wielded_weapon} {%0};
  #send !wield %0;
}

#alias wiki {
  #system {open http://wiki.maybeits.us/index.php/};
}
