#alias read_tin_files {
  #script {tin_list} {
    find ./%1 -wholename '*.tin' | grep -v init
  };

  #foreach { $tin_list[%*] } {file} {
    #if {@length{$file} > 0} {
      #showme reading $file;
      #read $file;
    };
  };

  #unvar tin_list;
}

#nop Do a fuzzy search over non tin/git fils;

#alias fsearch {
  #if {"%1" == ""} {
    #showme <170>Need to provide something to search for.;
    #return;
  };

  #script {fshow_files} {
    grep -rliI --exclude '*map.txt' --exclude '*.git*' --exclude '*.tin*' '%0' ./;
  };

  #foreach { $fshow_files[%*] } {file} {
    #if {"$file" != ""} {
      #system { cat '$file' };
      #showme <170>From file: $file\n;
    }
  }
}

#nop Look for specific files and show them;

#alias fcat {
  #if {"%1" == ""} {
    #system { ls -al };
  };
  #else {
    #script {files_found} {
      find ./ -wholename "*%1*" | grep -v .git | head -n 1
    };
    #var first_file_found $files_found[1];
    #system {
      if [ -n "${first_file_found}" ]; then
        msg="first file found: ${first_file_found}";
        printf "\e[97m$msg\e[0m\n";
      fi;
      if [ -d ${first_file_found} ]; then
        ls -al %1;
      elif [ -f ${first_file_found} ]; then
        cat %1;
      fi;
    };
    #unvar first_file_found;
  };
}

#alias grep {
  #if {"%1" != ""} { #system grep -r '%1' ./%2; }
}

