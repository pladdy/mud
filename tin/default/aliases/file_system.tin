#nop Grep files for a search pattern;

#alias gfiles {
  #if {"%1" == ""} {
    #showme <170>Need to provide something to search for.;
    #return;
  };

  #script {fshow_files} {
    grep -rliI --exclude-dir .git --exclude-dir tin --exclude '*.log' '%0' ./;
  };

  #foreach { $fshow_files[%*] } {file} {
    #if {"$file" != ""} {
      #showme <170>File: $file;

      #script {matches} {
        grep -rihI --exclude-dir .git --exclude-dir tin --exclude '*.log' '%0' "$file";
      };
      #foreach { $matches[%*] } {match} {#showme <170>  $match};

      #unvar matches;
    }
  }
  #showme <130>'fcat' <file> to see file.;

  #unvar fshow_files;
  #unvar match;
}

#nop Given a file, dump it to screen;

#alias fcat {
  #if {"%1" == ""} {
    print_system_output ls -al;
  };
  #else {
    #script {files_found} {
      find ./ -wholename "*%1*" | grep -v .git | head -n 1
    };
    #var first_file_found $files_found[1];

    #script {content} {
      if [ -n "${first_file_found}" ]; then
        msg="first file found: ${first_file_found}";
        printf "\e[97m$msg\e[0m\n";
      fi;
      if [ -d ${first_file_found} ]; then
        ls -al %1;
      elif [ -f ${first_file_found} ]; then
        cat %1;
      fi;
    };
    #foreach { $content[%*] } {line} {#showme $line};

    #unvar first_file_found;
    #unvar content;
  };


}

#nop Print the output of given script command;

#alias print_system_output {
  debug Output from '%0';
  #script {content} {%0};
  #foreach { $content[%*] } {line} {#showme $line};
  #unvar content;
}

#alias read_tin_files {
  #script {tin_list} {
    find ./%1 -wholename '*.tin' | grep -v '.bootstrap.tin';
  };

  #foreach { $tin_list[%*] } {file} {
    #if {@length{$file} > 0} {
      #showme reading $file;
      #read $file;
    };
  };

  #unvar tin_list;
}
