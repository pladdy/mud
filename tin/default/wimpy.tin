#nop Define wimpy_commands per guild profile;
#alias wimpy_commands {#nop;}

#alias wd {
  #switch {"%1"} {
    #case "n" {#send !wimpydir north};
    #case "s" {#send !wimpydir south};
    #case "e" {#send !wimpydir east};
    #case "w" {#send !wimpydir west};
    #case "nw" {#send !wimpydir northwest};
    #case "ne" {#send !wimpydir northeast};
    #case "sw" {#send !wimpydir southwest};
    #case "se" {#send !wimpydir southeast};
    #case "u" {#send !wimpydir up};
    #case "d" {#send !wimpydir down};
    #default {#send !wimpydir %0};
  }
}

#alias _default_wimpy_commands {
  #showme <110>Wimpy firing!;
  #send !hp;
  sound_beeps;
  #if {"$stepper" == "on"} {stepper off};
}

#action {^Your legs tried to run away, but failed.$} {
  _default_wimpy_commands;
  wimpy_commands;
}

#action {^Your legs run away with you!} {
  #showme <110>Wimpy was a random direction!;
  _default_wimpy_commands;
  wimpy_commands;
}

#action {^Your legs run away with you %1.$} {
  _default_wimpy_commands;

  #switch {"%1"} {
    #case "north" {#map move n};
    #case "south" {#map move s};
    #case "east" {#map move e};
    #case "west" {#map move w};
    #case "northwest" {#map move nw};
    #case "northeast" {#map move ne};
    #case "southwest" {#map move sw};
    #case "southeast" {#map move se};
    #case "up" {#map move u};
    #case "down" {#map move d};
    #default {#map move %1};
  };

  wimpy_commands;
}
