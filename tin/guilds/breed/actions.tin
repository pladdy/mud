#action {^%?%?H: %1/%2 S: %3/%4 P: %5/%6(%7) F: %8% SA: %9 E: %10 $} {

  #var using_psidraw {false};

  #var hp {%1};
  #var hp_max {%2};

  #var sp {%3};
  #var sp_max {%4};

  #var gp1 {%5};
  #var gp1_max {%6};
  #var reserve_psi {%7};

  #var gp2 {%8};
  #var gp2_max {100};

  #var enemy {%10};
  #if {"$enemy" == "None"} {#var {enemy} {0}};
  #replace {enemy} {%} {};

  #var gxp {0};

  attempt_mindsink;
  heal_low_psi;

  #if {$enemy > 0 && "$conflict" != "true" && $conflict_attempts < 3} {!conflict};
  show_status_bar;
}

#action {^C: %1 W: %2 D: %3% T: %4 A:%5$} {
  #var workings {%2};

  #if {"$contact_impossible" != "true"} {
    #if {$enemy > 0 && "$mindsink_empty" != "true"} {
      #regex {$workings} {Ct} {#nop;} {!contact};
    };
  }

  #nop Deactivate;

  #if {$gp1 < 500 && "$mindsink_empty" == "true"} {
    #regex {$workings} {Ct} {!contact stop;} {#nop};
    #regex {$workings} {Re} {!brepeat clear;} {#nop};
  }
}

#action {^The Glaetal crystal cannot hold anymore energy.$} {
  !hurl;
  !daystar 400;
}

#action {^You channel a fraction of the mental attack into the kulnara crystal.$} {
  #if {"$using_psidraw" != "true"} {
   !psidraw 500 from kulnara;
   #var using_psidraw {true};
  }
}

#action {^You try and channel the attack, but the crystal already overflows with energy.$} {
  #if {"$using_psidraw" != "true"} {
   !psidraw 500 from kulnara;
   #var using_psidraw {true};
  }
}

#action {^%1 has already been drained of psi.$} {
  #var mindsink_empty {true};
}

#action {^You are too far away to mindsink.$} {
  !contact stop;
}

#action {^You can't break through the mental barriers!!} {
  #var contact_impossible {true};
}

#action {^You delve into the subconcious of} {#var conflict true};
#action {^%1 has become resistant to your will.$} {#var conflict true};
#action {^That is far too strong for your mind to attack.$} {#var conflict true};
#action {^Something prevents you from creating conflict.$} {#var conflict true};

#action {^You attempt to create conflict within} {
  #math {conflict_attempts} {$conflict_attepts + 1};
};

#action {^You take advantage of your egress and take refuge from battle.$} {
  #map move $egress_dir;
  !contact stop;
  stepper off;
}

#action {^You are mentally too tired to perform this task.} {
  #if {$corpses > 0} {redis};
}

#action {^The weakness of your mind crosses the plane of thought and} {
  #if {$corpses > 0} {redis};
}

#action {^From afar, %1 pings you.$} {
  #format {requesting_player} {%l} {%1};
  !str $requesting_player;
}

#action {^You feel the force of a mental assault on your mind.$} {
  #if {"$contain_active" != "true"} {
    !contain;
    #var contain_active true;
  }
}
