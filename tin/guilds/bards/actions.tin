#action {^%!{[> ]*}H: %1/%2 S: %3/%4 K: %5/%6 V: %7/100%% S: %8(%9%%) E: %10} {
  #var hp {%1};
  #var hp_max {%2};

  #var sp {%3};
  #var sp_max {%4};

  #var gp1 {%5};
  #var gp1_max {%6};

  #var gp2 {%7};
  #var gp2_max {100};

  #local enemy_raw {%10};
  #replace {enemy_raw} {T:} {};
  #replace {enemy_raw} {% } {};
  #if {"$enemy_raw" == " "} {#var enemy 0} {#var enemy $enemy_raw};

  #if {$enemy > 0} {attempt_tb};
  #if {$gp1 < 50 && $corpses >0} {redi};
}

#action {^%!{[> ]*}g2n/g2s: %1%%/%2 A:%3} {
  #var gxp %2;
  #var {effects} %3;
  #replace {effects} {[} {};
  #replace {effects} {]} {};

  #nop Capture song/spell effects;
  #regexp {$effects} { O:%d } {#var songs[o_muse] &1}
                              {#var songs[o_muse] 0};
  #regexp {$effects} { PE:%d } {#var spells[pe] &1}
                                {#var spells[pe] 0};
  #regexp {$effects} { PG:%d } {#var spells[pg] &1}
                                {#var spells[pg] 0};
  #regexp {$effects} { PN:%d } {#var spells[pn] &1}
                                {#var spells[pn] 0};
  #regexp {$effects} { HS:%d } {#var spells[hard_skin] &1}
                               {#var spells[hard_skin] 0};
  #regexp {$effects} { PF:%d } {#var spells[protection_frost] &1}
                               {#var spells[protection_frost] 0};
  #regexp {$effects} { PH:%d } {#var spells[protection_heat] &1}
                               {#var spells[protection_heat] 0};
  #regexp {$effects} { P:%d } {#var performing true}
                              {#var performing false};
  #regexp {$effects} { MB } {#var spells[mind_blank] 1}
                            {#var spells[mind_blank] 0};

  #if {$enemy > 0} {
    attempt_soothe;

    attempt_hardened_skin;
    attempt_protection_align;
    #nop attempt_protection_heat;
    #nop attempt_protection_frost;
    attempt_mind_blank;

    #nop attempt_hypnotic_pattern;
    #nop attempt_noxious_cloud;
    attempt_combat_spell;

    attempt_o_muse;
  };
  show_status_bar;
}

#nop Protection from <alignment> spells;

#action {^%!{[> ]*}Your protection from %1 has faded.$} {
  #var protection_align[%1] off;
}

#action {^%!{[> ]*}Your protection from %1 has been dispelled!$} {
  #var protection_align[%1] off;
}

#action {^%!{[> ]*}You cast protection from %1 on yourself!$} {
  #var protection_align[%1] on;
}

#nop Hypnotic pattern;

#action {^%!{[> ]*}%1 stands around staring at the magical pattern!$} {
  #var hypnotic_pattern on;
}

#action {^%!{[> ]*}That has no chance of affecting such a creature.$} {
  #var hypnotic_pattern on;
}

#action {^%!{[> ]*}You lose your song.$} {
  !resume;
}

#nop Mind Blank;

#action {^%!{[> ]*}Your mind feels suddenly more vulnerable.$} {
  attempt_mind_blank;
}

#nop Shield actions;

#action {^%!{[> ]*}You can't play a song with an instrument and use a shield at the same time!$} {
  !remove shield;
  !resume;
}

#action {^%!{[> ]*}Not while you're carrying a shield!$} {
  !remove shield;
  !resume;
}

#nop #action {^%!{[> ]*}You cease your performance of} {!wear shield}
#nop #action {^%!{[> ]*}You finish your performance with a bow.} {!wear shield}

#action {^%!{[> ]*}You wave your hands around, make swooshing and farting sounds, and spit$} {
  #var noxious_cloud on;
}

#action {^%!{[> ]*}That creature is already somewhat spasmodic.$} {
  #var spasmodic_shuffle on;
}

#action {^%!{[> ]*}With a few quick mystical words you send Sylvan Elf into a spastic fit!$} {
  #var spasmodic_shuffle on;
}

#action {^%!{[> ]*}Perform song for whom?$} {
  ce;
}
