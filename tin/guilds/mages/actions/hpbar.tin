#action {^%!{[> ]*}H: %1/%2 S: %3/%4(%5%) Ge: %6% S|C: %7%|%8% G|M: %9|%10(%11%) Sc: %12(%13%)} {
  #var hp {%1};
  #var hp_max {%2};

  #var sp {%3};
  #var sp_max {%4};
  #replace {sp_max} {S} {};

  #var mage_gem {%6};
  #if {$mage_gem < 80 && "$mage_familiar_present" == "true"} {#send !ffetch gem};

  #nop flip the percentages for status bar;
  #math {gp1} {(%7 - 100) * -1};
  #var gp1_max {100};

  #math {gp2} {(%8 - 100) * -1};
  #var gp2_max {100};

  #var golems %9;
  #var mystic_immersions %10;
  #var guild_reset %11;
  #var school_spells %12;
  #var school_reset %13;

  attempt_mystic_immersion;
  attempt_explosive_release;
  attempt_focus_mind;

  check_saturation;
  check_concentration;
}

#alias _corpses {
  #var corpses %1;
  #var max_corpses %2;
  #if {$corpses < max_corpses} {#var coffin_status non_full};
}

#alias _enemy_status {
  #var enemy {%1};
  #replace {enemy} {%} {};
  #math {rounds_in_combat} {$rounds_in_combat + 1};
}

#alias _spell_tap_status {
  #format spell_tap_status {%p} {%1};
  #replace {spell_tap_status} { } {};
  #format spell_taps {%L} {$spell_tap_status};
}

#alias _handle_hpbar2 {
  #var gxp %1;

  #nop  Ensure these spells are always active;
  #regex {$dspells} {SS} {#nop} {#send !cast stoneskin};
  #regex {$dspells} {MG} {#nop} {#send !cast major globe};
  #regex {$dspells} {MS} {#nop} {#send !cast magnificent shield};
  #regex {$dspells} {A} {#nop} {#send !cast armor};
  #regex {$dspells} {*I} {#var immersed true} {#var immersed false};

  #nop Spells to monitor if i enabled them;
  #if {"$spells[pg]" == "on"} {#regex {$dspells} { PG} {#nop} {pg}};
  #if {"$spells[pe]" == "on"} {#regex {$dspells} { PE} {#nop} {pe}};
  #if {"$spells[B]" == "on"} {
    #regex {$dspells} { B}  {#nop} {#send !cast blink}
  };
  #if {"$spells[pa]" == "on"} {
    #regex {$dspells} { PA} {#nop} {#send !cast prismatic aura};
  };

  #nop  Keep some corpses tapped to handle saturation accumulation;
  #if {$spell_taps < 3 && $gp1 < 25 && $corpses > 0} {rebl};

  attempt_slow;
  attempt_shelter;
  show_status_bar;
  check_stepper;
}

#action {^%!{[> ]*}Gx: %1% P:%2 St:%3 C: %4/%5 Mon(%6):%7} {
  #var dspells %2;
  _spell_tap_status %3;
  _enemy_status %7;
  _corpses %4 %5;
  _handle_hpbar2 %1;
} {4}

#action {^%!{[> ]*}Gx: %1% P:%2 St:%3 C: %4/%5} {
  #var enemy 0;
  #var rounds_in_combat 0;
  #var dspells %2;
  _spell_tap_status %3;
  _corpses %4 %5;
  _handle_hpbar2 %1;
} {5}

#action {^%!{[> ]*}Gx: %1% P:%2 St:%3 C: None Mon(%4):%5} {
  #var dspells %2;
  _spell_tap_status %3;
  _enemy_status %5;
  _handle_hpbar2 %1;
} {4}

#action {^%!{[> ]*}Gx: %1% P:%2 St:%3 C: None} {
  #var enemy 0;
  #var rounds_in_combat 0;
  #var dspells %2;
  _spell_tap_status %3;
  _handle_hpbar2 %1;
} {5}