#action {^H: %1/%2 S: %3/%4(%5%) Ge: %6% S|C: %7%|%8% G|M: %9|%10(%11)} {
  #var hp {%1};
  #var hp_max {%2};

  #var sp {%3};
  #var sp_max {%4};
  #replace {sp_max} {S} {};

  #nop flip the percentages for status bar;
  #math {gp1} {(%7 - 100) * -1};
  #var gp1_max {100};

  #math {gp2} {(%8 - 100) * -1};
  #var gp2_max {100};

  #var golems %9;
  #var mystic_immersions %10;

  saturation_check;
  concentration_check;
  attempt_mystic_immersion;
  stepper_check;
}

#action {^Gx: %1% P:%2 St:%3 Mon(%4):%5} {
  #var gxp {%1};

  #format spell_tap_status {%p} {%3};
  #replace {spell_tap_status} { } {};
  #format spell_taps {%L} {$spell_tap_status};

  #nop keep some corpses tapped to handle saturation accumulation;
  #if {$spell_taps < 2 && $gp1 < 50 && $corpses > 0} {rebl};

  #var enemy {%5};
  #replace {enemy} {%} {};

  #var dspells {%2};
  #regex {$dspells} { A} {#nop} {!cast armor};
  #regex {$dspells} { MS} {#nop} {!cast magnificent shield};
  #regex {$dspells} { SS} {#nop} {!cast stoneskin};
  #regex {$dspells} { MG} {#nop} {!cast major globe};
  #regex {$dspells} { B}  {#nop} {!cast blink};
  #regex {$dspells} { *I} {#var immersed true} {#var immersed false};

  #nop Spells to monitor if i enabled them;
  #if {"$spells[pg]" == "on"} {#regex {$dspells} { PG} {#nop} {pg}};
  #if {"$spells[pe]" == "on"} {#regex {$dspells} { PE} {#nop} {pe}};
  show_status_bar;
} {4}

#action {^Gx: %1% P:%2 St:%3} {
  #var enemy {0};
  #var dspells {%2};
  show_status_bar;
} {5}
