#action {^%!{[> ]*}H: %1/%2 S: %3/%4(%5%) Ge: %6% S|C: %7%|%8% G|M: %9|%10(%11%) Sc: %12(%13%)} {
  #var hp {%1};
  #var hp_max {%2};

  #var sp {%3};
  #var sp_max {%4};
  #replace {sp_max} {S} {};

  #nop flip the percentages for status bar;
  #math {gp1} {(%7 - 100) * -1};
  #var gp1_max {100};

  #math {gp2} {(%8 - 100) * -1};
  #var gp2_max {100};

  #var golems %9;
  #var mystic_immersions %10;
  #var guild_reset %11;
  #var school_spells %12;
  #var school_reset %13;

  attempt_mystic_immersion;
  attempt_explosive_release

  check_saturation;
  check_concentration;
  check_stepper;
}

#action {^%!{[> ]*}Gx: %1% P:%2 St:%3 Mon(%4):%5} {
  #var gxp {%1};

  #format spell_tap_status {%p} {%3};
  #replace {spell_tap_status} { } {};
  #format spell_taps {%L} {$spell_tap_status};

  #nop keep some corpses tapped to handle saturation accumulation;
  #if {$spell_taps < 3 && $gp1 < 30 && $corpses > 0} {rebl};

  #var enemy {%5};
  #replace {enemy} {%} {};

  #var dspells {%2};
  #nop #regex {$dspells} { S} {#nop} {#send !cast shield};
  #nop #regex {$dspells} { RS} {#nop} {#send !cast rhino skin};
  #nop #regex {$dspells} { mg} {#nop} {#send !cast minor globe};

  #regex {$dspells} { SS} {#nop} {#send !cast stoneskin};
  #regex {$dspells} { MG} {#nop} {#send !cast major globe};
  #regex {$dspells} { MS} {#nop} {#send !cast magnificent shield};
  #regex {$dspells} { B}  {#nop} {#send !cast blink};
  #regex {$dspells} { PA} {#nop} {#send !cast prismatic aura};

  #regex {$dspells} { A} {#nop} {#send !cast armor};
  #regex {$dspells} { *I} {#var immersed true} {#var immersed false};

  #nop Spells to monitor if i enabled them;
  #if {"$spells[pg]" == "on"} {#regex {$dspells} { PG} {#nop} {pg}};
  #if {"$spells[pe]" == "on"} {#regex {$dspells} { PE} {#nop} {pe}};

  attempt_slow;
  show_status_bar;
} {4}

#action {^%!{[> ]*}Gx: %1% P:%2 St:%3} {
  #var enemy {0};
  #var dspells {%2};
  show_status_bar;
} {5}
