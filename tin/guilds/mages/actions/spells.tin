#action {^%!{[> ]*}----*> Your %1 contingency has dissipated!<*----} {
  #switch {"%1"} {
    #case {"stoneskin"} {#send !cast contingency as %1#cast stoneskin};
    #case {"shield"} {#send !cast contingency as %1#cast magnificent shield};
    #case {"blinking"} {#send !cast contingency as blink#cast blink};
    #case {"major_globe"} {#send !cast contingency as major_globe#cast major globe};
    #default {#send !cast contingency as %1#cast %1};
  };
}

#action {^%!{[> ]*}Your protection from %1 has ended.$} {
  #if { $rounds_in_combat > 0 } {
    #if {"%1" == "good" && "$spells[pg]" == "on"} {pg};
    #if {"%1" == "evil" && "$spells[pe]" == "on"} {pe};
  };
}

#nop  Mystic Immersion;

#action {^%!{[> ]*}With great magical precision you tap directly into the rivers of magical} {
  #send !ash off;
}

#action {^%!{[> ]*}Your immersion has expired.$} {
  #send !cast spell tap always;
  #2 #send !ash;
}

#nop  Summons;

#action {^%!{[> ]*}You wave your hands calling forth summoning magics and draw} {
  #var offense_haste false;
  #var defense_haste false;
  #var offense_mf false;
  #var defense_mf false;
};

#action {^%!{[> ]*}The golem shudders for a moment, then falls to the ground in a heap.} {
  #var defense_haste false;
  #var defense_mf false;
}

#nop  Haste;

#action {^%!{[> ]*}You feel slower as the haste wears off.$} {
  #var haste false;
  #nop #if {$rounds_in_combat > 0} {#send !cast haste};
}

#action {^%!{[> ]*}You raise your arms and suddenly %1 is moving much faster!$} {
  #var haste true;
}

#action {^%!{[> ]*}You raise your arms and suddenly %1 golem is moving much faster!$} {
  #var defense_haste true;
}

#action {^%!{[> ]*}You raise your arms and suddenly %1 is moving much faster!$} {
  #var offense_haste true;
}

#action {^%!{[> ]*}%1 golem is already hasted.$} {
  #var defense_haste true;
}

#action {^%!{[> ]*}%1 is already hasted.$} {
  #format {target} {%l} {%1};
  #if {"$target" == "$character"}{#var haste true};
  #else {#var offense_haste true};
}

#action {^%!{[> ]*}%1 (%2 Golem) seems to slow down...$} {
  #var defense_haste false;
} {2}

#action {^%!{[> ]*}%1 %2 seems to slow down...$} {
  #var offense_haste false;
} {3}

#action {^%!{[> ]*}magical words.  A creature forms from the materials!$} {
  #var defense_haste false;
}

#action {^%!{[> ]*}Cast haste on whom?$} {
  #var defense_haste true;
  #var offense_haste true;
}

#nop  Monstrous Fortitude;

#action {^%!{[> ]*}As your spell weaves its pattern %1 (%2 Golem) [defense]} {
  #var defense_mf true;
}

#action {^%!{[> ]*}As your spell weaves its pattern %1 [offense]} {
  #var offense_mf true;
}

#action {%1 (%2 Golem) seems to grow smaller.} {
  #var defense_mf false;
}

#action {%1 seems to grow smaller.} {
  #var offense_mf false;
}

#nop Phantasmal Killer;
#action {^%!{[> ]*}You wave your hands and *POOF*} {#var phantasmal_killer casted}

#action {^%!{[> ]*}-*> The something begins to fizzle as the illusion is discovered! <*-$} {
  #var phantasmal_killer discovered;
}

#nop Slow;
#action {^%!{[> ]*}You raise your arms and suddenly %1 is moving much slower!$} {
  #var slowed 1;
}

#action {^%!{[> ]*}%1 is already slowed.$} {#var slowed 1}

#action {^%!{[> ]*}%1 seems to speed up...$} {
  #var slowed 0;
  #send !cast slow;
}

#action {^%!{[> ]*}That creature cannot be affected!.$} {#var slowed 1}

#action {^%!{[> ]*}You wave your hands and anchor yourself to the spot!$} {
  #var anchored true;
}

#action {^%!{[> ]*}Your anchor has been dispelled!$} {
  #var anchored false;
}

#action {^%!{[> ]*}You wave your hands and a glowing orb appears!} {
  #send !keep orb;
}
