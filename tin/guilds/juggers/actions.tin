#action {^%!{[> ]*}H: %1/%2 S: %3/%4 St: %5% He: %6% C/N: %7/%8 [%9%] Hi: %10 E: %11%?$} {
  #var hp {%1};
  #var hp_max {%2};

  #var sp {%3};
  #var sp_max {%4};

  #var gp1 {%5};
  #var gp1_max {100};

  #math gp2 {100 - %6};
  #var gp2_max {100};

  #var clan_powers %7;
  #var missiles %8;

  #var enemy_str {%11};
  #regexp {$enemy_str} {{\d+}} {#var enemy &1} {#var enemy 0};

  #nop some mobs health can't be scanned, their health will start as 'perfect';
  #format enemy_str_len %L $enemy_str;
  #if {$enemy_str_len > 3} {#var enemy 100};

  #if {$enemy > 0 && $gp1 < 30 && $corpses > 0} {rede};

  #nop Use up excess sps on heat if i'm at full;
  #if {$enemy > 0 && $gp2 < 75 && $sp >= $sp_max} {#send !dissipate};

  attempt_airdrop;
}

#action {^%!{[> ]*}G: %1% A: %2 C: %3 JJ: %4 LL: %5} {
  #var gxp %1;
  #var corpses %3;

  #if {"%2" != ""} {
    #var jugger_ammo %2;
    #regexp {$jugger_ammo} {{\d+}} {#var jugger_mounts[0] &1};
    #regexp {$jugger_ammo} {{\d+}/{\d+}} {#var jugger_mounts[1] &2} {#var jugger_mounts[1] 101};

    #if {$jugger_mounts[0] < 100} {rammo 5 1};
    #if {$jugger_mounts[1] < 100} {rammo 5 2};
  };
  #else {
    #var jugger_ammo 0;
  };

  check_stepper;
  show_status_bar;
}

#action {^%!{[> ]*}%1 rawrs menacingly at you.  RAAWWWRRR!} {
  #format {player} {%l} {%1};
  #send !frenzy $player;
  #unvar player;
}

#action {^%!{[> ]*}%1 gives you a high-five!$} {
  #format {player} {%l} {%1};
  #send !airdrop $player;
  #unvar player;
}

#action {^%!{[> ]*}From afar, %1 rawrs menacingly at you.  RAAWWWRRR!} {
  #format {player} {%l} {%1};
  #send !frenzy $player;
  #unvar player;
} {4}

#action {^%!{[> ]*}From afar, %1 gives you a high-five!$} {
  #format {player} {%l} {%1};
  #send !airdrop $player;
  #unvar player;
} {4}

#gag {^%!{[> ]*}The computer chirps 'No healing needed.'}
