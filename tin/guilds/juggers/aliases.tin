#alias ammo {
  run jugger_ammo;
  #switch {"%1"} {
    #case {"proj"} {#send !vbuy projectile 4 1 10000};
    #case {"miss"} {#send !vbuy missile 1 1 10000};
    #default {#showme pass in 'miss' or 'proj'};
  }
}

#alias attempt_airdrop {
  #math {sp_ratio} {$sp / $sp_max * 1.0};

  #if {$clan_powers > 0 && $enemy > 0 && $gp2 < 60 && $sp_ratio < 0.3} {
    #send !airdrop;
  };
  #elseif {$clan_powers > 0 && $enemy > 0 && $gp1 < 50 && $corpses < 1} {
    #send !airdrop;
  };
}

#alias login_commands {
  #delay 2 #send !support summon;
  #delay 4 #send !support assault fire;
  #delay 6 #send !empower on;
  #delay 8 set_afk;
}

#alias f10 {use_corpse}

#alias f12 {
  #switch {$map[current_room]} {
    #case {11437} {
      #loop 2 $BAGS_TO_USE i {#send !donate all from bag $i};
    };
    #case {11438} {
      #loop 2 $BAGS_TO_USE i {#send !donate all from bag $i};
    };
    #default { #loop 2 $BAGS_TO_USE i {#send !sell all from bag $i} };
  };
}

#nop  Remote Ammo (via truck) purchase;
#alias rammo {
  #if {"%1" == ""} {
    #send !truck ammo list projectile;
  };
  #else {
    #send !truck ammo vbuy projectile %1 %2 10000;
  };
}

#alias rede {
  #send !unwrap;
  #send !fdeener;
}

#alias set_afk {
  log.info Setting afk for juggers to get an 'airdrop';
  #send !afk Juggers, if you need an airdrop: 'high5' me;
}

#alias su_can {
  #send !support reset;
  #99 #send !support upgrade powerplant;
  #99 #send !support upgrade mount;
  #send !support assault fire,radiation;
}

#alias su_spam {
  #if {"%1" == "on"} {
    log.info Ansi ON for jugger support hits.;
    #send !aset jugg_support bkred;
  };
  #else {
    log.info Ansi OFF for jugger support hits.;
    #send !aset jugg_support hide;
  }
}

#alias su_tank {
  #send !support reset;
  #99 #send !support upgrade armor;
  #99 #send !support upgrade repair;
  #send !support defend edged,blunt,fire,ice,electric;
}

#alias use_corpse {
  #send !hp;
  #send !fdeener;
  #send !hp;
  #send !get all;
}
