#alias ammo {
  run jugger_ammo;
  #switch {"%1"} {
    #case {"proj"} {#send !vbuy projectile 4 1 10000};
    #case {"miss"} {#send !vbuy missile 1 1 10000};
    #default {#showme pass in 'miss' or 'proj'};
  }
}

#alias attempt_airdrop {
  #math {sp_ratio} {$sp / $sp_max * 1.0};

  #if {$clan_powers > 0 && $rounds_in_combat > 0 && $gp2 < 60 && $sp_ratio < 0.3} {
    #send !airdrop;
  };
  #elseif {$clan_powers > 0 && $rounds_in_combat > 0 && $gp1 < 30 && $corpses < 1} {
    #send !airdrop;
  };
}

#alias check_stepper {
  #if {$corpses < 2 && $gp1 < 20 && "$stepper" == "on"} {stepper off};
  #else {
    #format {current_time} {%T};
    #math {time_since_stepper_started} {$current_time - $stepper_started};

    #if {"$stepper" == "on" && $time_since_stepper_started > 750} {
      stepper off;

      log.info Creating delay 'reactivate_stepper' to activate stepper after regen.;

      #nop Round is seconds * 2 (2s / round);
      #local min_seconds 10;
      #math rando {1 d 20};
      #math regen_time {$min_seconds + $rando};

      #delay {reactivate_stepper} {
        log.info "Re-activating stepper after regen time: $regen_time seconds.";
        stepper on;
      } {$regen_time};

      #unvar rando;
    };
  };
}

#alias f10 {use_corpse}

#alias f12 {
  #switch {$map[current_room]} {
    #case {11437} {
      #loop 2 $BAGS_TO_USE i {
        #send !get all from bag $i;
        #send !donate all;
      };
    };
    #case {11438} {
      #loop 2 $BAGS_TO_USE i {
        #loop 2 $BAGS_TO_USE i {
          #send !get all from bag $i;
          #send !donate all;
        };
      };
    };
    #default { #loop 2 $BAGS_TO_USE i {#send !sell all from bag $i} };
  };
}

#alias k {#send !kill %0; #send !ki %0;}

#alias login_commands {
  #delay 2 #send !support summon;
  #delay 4 #send !support assault fire;
  #delay 6 #send !empower on;
  #delay 8 set_afk;
}

#alias mip_combat_hook {
  #if {$rounds_in_combat > 0 && $gp1 < 30 && $corpses > 0} {rede};

  #nop Use up excess sps on heat if i'm at full;
  #if {$rounds_in_combat > 0 && $gp2 < 75 && $sp >= $sp_max} {#send !dissipate};

  #nop Use up missiles and clan powers;
  #if {$clan_powers > 0 && $jugger_reset > 97} {#send !frenzy;};
  #if {$missiles > 0 && $rounds_in_combat > 0 && $jugger_reset > 97} {#send !fire $missiles};

  attempt_airdrop;
  check_stepper;
};

#alias mip_gline1_hook {
  #local data %0;
  #nop Example:
  #nop Ammo:[500/] <yGxp: 83.5001%> Hits: [0] <cCoffin: N/A>;

  #regexp {$data} {Ammo:[{\d+}/]} {#var jugger_mounts[0] &1};
  #regexp {$data} {Ammo:[{\d+}/{\d+}]} {#var jugger_mounts[1] &2} {#var jugger_mounts[1] 101};

  #if {$jugger_mounts[0] < 100} {rammo 4 1};
  #if {$jugger_mounts[1] < 100} {rammo 4 2};

  #regexp {$data} {Gxp: {\d+\.\d+}%} {#var gxp &1};
  calculate_delta gxp;

  #regexp {$data} {Coffin: {\d+}} {#var corpses &1} {#var corpses 0};
}

#alias mip_gline2_hook {
  #local data %0;
  #nop Example;
  #nop <rNukes: [3/3]> <vSupport: [3/4]> Reset: [65%] Suit: Elemental;
  #regexp {$data} {Nukes: [{\d+}/} {#var missiles &1};
  #regexp {$data} {Support: [{\d+}/} {#var clan_powers &1};
  #regexp {$data} {Reset: [{\d+}%]} {#var jugger_reset &1};
}

#nop  Remote Ammo (via truck) purchase;
#alias rammo {
  #if {"%1" == ""} {
    #send !truck ammo list projectile;
    #showme <170>Syntax is 'rammo <ammo number> <mount number>';
  };
  #else {
    #send !truck ammo vbuy projectile %1 %2 10000;
  };
}

#alias rede {
  #send !unwrap;
  #send !fdeener;
}

#alias set_afk {
  log.info Setting afk for juggers to get an 'airdrop';
  #send !afk Juggers, if you need an airdrop: 'high5' me;
}

#alias su_can {
  #send !support reset;
  #99 #send !support upgrade powerplant;
  #110 #send !support upgrade mount;
  #send !support assault fire,radiation,electric,ice,poison;
}

#alias su_def {
  #send !support defend edged,blunt,fire,ice,electric;
}

#alias su_spam {
  #if {"%1" == "on"} {
    log.info Ansi ON for jugger support hits.;
    #send !aset jugg_support bkred;
  };
  #else {
    log.info Ansi OFF for jugger support hits.;
    #send !aset jugg_support hide;
  }
}

#alias su_tank {
  #send !support reset;
  #99 #send !support upgrade armor;
  #99 #send !support upgrade repair;
  #send !support defend edged,blunt,fire,ice,electric toggle;
}

#alias use_corpse {
  #send !hp;
  #send !fdeener;
}
