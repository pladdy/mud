#alias actuate {
  run cyborg_actuator;
  #send actuate %0;
  f2;
}

#alias check_stepper {
  #if {$corpses < 1 && "$stepper" == "on"} {stepper off};
  #else {
    #format {current_time} {%T};
    #math {time_since_stepper_started} {$current_time - $stepper_started};
  
    #if {"$stepper" == "on" && ($time_since_stepper_started > 750 || $corpses < 7)} {
      stepper off;
  
      log.info Creating delay 'reactivate_stepper' to activate stepper after regen.;
  
      #nop Round is seconds * 2 (2s / round);
      #local min_seconds 20;
      #math rando {1 d 20};
      #math regen_time {$min_seconds + $rando};
  
      #delay {reactivate_stepper} {
        log.info "Re-activating stepper after regen time: $regen_time seconds.";
        stepper on;
      } {$regen_time};
  
      #unvar rando;
    };
  };
}

#alias cos {
  #send !unwield $wielded_weapon;
  #send !configure strategy %0;
  #send !wield $wielded_weapon;
}

#alias redc {
  #send !unwrap smallest;
  #send !decompose;
}

#alias reet {
  #send !unwrap smallest;
  #send !eat corpse;
}

#alias reload_configure {
  #textin tin/guilds/cyborgs/configure.txt
}

#alias use_corpse {
  #if {$gp1 == $gp1_max && "$use_spoon" == "true"} {#send !eat corpse};
  #else {#send !decompose};
  #send !hp;
}
