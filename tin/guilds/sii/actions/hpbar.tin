#action {^H: %1/%2 [R: %3 O: %4 (%5)] S: %6/%7 F: %8/%9 C: %10/%11 E: %12} {
  #var hp {%1};
  #var hp_max {%2};

  #var sp {%6};
  #var sp_max {%7};

  #var gp1 {%8};
  #var gp1_max {%9};

  #var gp2 {%10};
  #var gp2_max {%11};

  #var enemy_str {%12};
  #regexp {$enemy_str} {(%d} {#var enemy &1};

  #if {"$enemy_str" == "None"} {
    #var enemy {0};
    #var enemy_last {0};
  };

  #nop Pick a regen rate based on current hp percent and regen setting;
  #math {hp_as_pct} {($hp * 1.00)/ $hp_max * 100};

  #var regen_rate @long_regen_name{%3};
  #var regen_level @regen_level{$regen_rate};

  #if {$hp_as_pct > 95 && $regen_level > 0} {
    !focus regeneration normal;
  };
  #else {
    #if {$hp_as_pct < 95 && $regen_level < 1} {
      !focus regeneration quick;
    };
    #elseif {$hp_as_pct < 91 && $regen_level < 2} {
      !focus regeneration rapid;
    };
    #elseif {$hp_as_pct < 87 && $regen_level < 3} {
      !focus regeneration fast;
    };
    #elseif {$hp_as_pct < 83 && $regen_level < 4} {
      !focus regeneration hyper;
    };
    #elseif {$hp_as_pct < 79 && $regen_level < 5} {
      !focus regeneration blazing;
    };
  };

  #nop Warn if control is low;
  #math {control_as_pct} {($gp2 * 1.00)/ $gp2_max * 100};
  #if {$control_as_pct < 10} {#showme <110>CONTROL IS LOW!!!};
}

#action {^Gx: %1 LG} {
  #var gxp {%1};
  show_status_bar;
}

#action {^%?%sYou %1 your metabolism, and focus on regenerating at a %2 rate.%s$} {
  #nop #var regen_rate {%2};
}

#action {^%?%sYour metabolism is already regenerating at a %1 rate.%s$} {
  #nop #var regen_rate {%1};
}
