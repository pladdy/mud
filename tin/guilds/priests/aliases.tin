#alias attempt_all_the_things {
  attempt_effects;
  attempt_curse;
  #nop attempt_rift;
  #nop attempt_beckon;
  #nop attempt_growth;
  attempt_offer;
  attempt_beseech;
}

#alias attempt_beckon {
  #if {"$beckon" == "OFF" && $enemy > 0 && $sp > 200} {#send !beckon};
}

#alias attempt_beseech {
  #if {$gp1 < 50 && $beseeches > 0} {#send !beseech};
}

#alias attempt_curse {
  #if {"$curse" == "OFF" && $enemy > 0 && $sp > 150} {#send !curse};
}

#alias attempt_effects {
  #if {$enemy > 0} {
    #regex {$spells} {PR} {#nop} {#send !protection};
    #regex {$spells} {VE} {#nop} {#send !vestment};
    #regex {$spells} {BA} {#nop} {#send !barkskin};
    #regex {$spells} {BB} {#nop} {#send !blade};
    #regex {$spells} {HA} {#nop} {#send !haste};
    #regex {$spells} {BL} {#nop} {#send !bless};
    #nop #regex {$spells} {BE} {#nop} {#send !beneficence};
  };
}

#alias attempt_growth {
  #if {"$beckon" == "ON" && "$growth" == "OFF" && $enemy > 0 && $sp > 200} {
    #send !growth demon;
  };
}

#alias attempt_inspirit {
  #nop don't need with contingency at glvl 15;
  #if {$enemy > 0 && ($sp < 50 || ($sp < $sp_max && $gp1 == $gp1_max))} {
    #2 ins
  };
}

#alias attempt_meditate {
  #math {min_mana_for_meditate} {$gp1_max * .7};
  #nop don't meditate if mana is high enough or piety is not max;
  #if {$gp1 < $min_mana_for_meditate || $gp2 < $gp2_max} {m};
}

#alias attempt_offer {
  #if {$gp1 < 150 && $corpses > 0 && $enemy > 0} {reof};
}

#alias attempt_protection {
  #if {"$protection_status" == "OFF" && $enemy > 0 && $sp > 150} {#send !protection};
}

#alias attempt_rift {
  #if {"$rift" == "OFF" && $enemy > 0 && $sp > 400} {#send !rift};
}

#alias commands_after_corpse {
  attempt_meditate;
  #var curse OFF;
  #var beckon OFF;
  #var growth OFF;
  #var rift OFF;
}

#alias deup_spell {
  #var deup_regex %1;
  #var deup_spell %2;

  #delay $deup_delay {
    #regex {$spells} {$deup_regex} {#nop} {
      #send !$deup_spell;
      #math deup_delay {$deup_delay + 2};
    };
  };
}

#alias deup {
  #var i 0;
  #regexp {$spells} {PR} {#nop;} {
    #delay {$i} {#send !protection}; #math {i} {$i + 2};
  };
  #regexp {$spells} {VE} {#nop;} {
    #delay {$i} {#send !vestment}; #math {i} {$i + 2};
  };
  #regexp {$spells} {BA} {#nop;} {
    #delay {$i} {#send !barkskin}; #math {i} {$i + 2};
  };
  #regexp {$spells} {BB} {#nop;} {
    #delay {$i} {#send !blade}; #math {i} {$i + 2};
  };
  #regexp {$spells} {HA} {#nop;} {
    #delay {$i} {#send !haste}; #math {i} {$i + 2};
  };
  #regexp {$spells} {BL} {#nop;} {
    #delay {$i} {#send !bless}; #math {i} {$i + 2};
  };
  #nop #regex {$spells} {BE} {#nop} {#send !beneficence};
}

#alias enable_panics {
  #send !contingency on;
  #nop #send !paction on;
}

#alias f10 {use_corpse}

#alias f12 {
  #if {$map[current_room] == 22052 || $map[current_room] == 22577} {
    #loop 2 12 i {
      #send !get all from bag $i;
      #send !sacrifice all;
    };
  };
  #else {
    #loop 2 12 i {#send !sell all from bag $i};
  }
}

#alias m {
  #nop #send !contingency off;
  #nop #send !paction off;
  #send !meditate;
}

#alias process_enemy_status {
  #regexp {$enemy_str} { : %!{\d}} {
    #var enemy &1;
    #replace {enemy} {%} {};
  };

  #regexp {$enemy_str} { : %w} {
    #var enemy &1;
    #replace {enemy} {%} {};
  };

  #switch {"$enemy"} {
    #case {"per"} {#var enemy 100};
    #case {"bru"} {#var enemy 70};
    #case {"ble"} {#var enemy 60};
    #case {"eme"} {#var enemy 10};
    #case {"Dea"} {#var enemy 5};
  };

  #if {"$enemy_str" == "none"} {#var enemy {0}};
}

#alias reof {
  #send !unwrap smallest;
  #send !offer corpse;
}

#alias use_corpse {
  #send !offer corpse;
  #send !hp;
  #send !get all;
}

#alias wimpy_commands {
  #delay 2 {attempt_meditate};
}
