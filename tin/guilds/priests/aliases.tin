#alias attempt_beseech {
  #if {$gp1 < 50 && $beseeches > 0} {#send !beseech};
}

#alias attempt_curse {
  #if {"$curse" == "OFF" && $enemy > 0 && $sp > 150} {#send !curse};
}

#alias attempt_effects {
  #if {$enemy > 0} {
    #regex {$spells} {PR} {#nop} {#send !protection};
    #regex {$spells} {VE} {#nop} {#send !vestment};
    #regex {$spells} {BA} {#nop} {#send !barkskin};
    #regex {$spells} {HA} {#nop} {#send !haste};
    #regex {$spells} {BL} {#nop} {#send !bless};
    #regex {$spells} {BE} {#nop} {#send !beneficence};
    #if {"$blade" == "OFF" && $enemy > 0} {#send !blade};
  };
}

#alias attempt_growth {
  #if {"$beckon" == "ON" && "$growth" == "OFF" && $enemy > 0 && $sp > 200} {
    #send !growth demon;
  };
}

#alias attempt_meditate {
  #math {min_mana_for_meditate} {$gp1_max * .7};
  #nop don't meditate if mana is high enough or piety is not max;
  #if {$gp1 < $min_mana_for_meditate || $gp2 < $gp2_max} {m};
}

#alias attempt_offer {
  #if {$gp1 < 150 && $corpses > 0} {reof};
}

#alias attempt_protection {
  #if {"$protection_status" == "OFF" && $enemy > 0 && $sp > 150} {#send !protection};
}

#alias attempt_rift {
  #if {"$rift" == "OFF" && $enemy > 0 && $sp > 400} {#send !rift};
}

#alias attempt_beckon {
  #if {"$beckon" == "OFF" && $enemy > 0 && $sp > 200} {#send !beckon};
}

#alias commands_after_corpse {
  attempt_meditate;
  #var curse OFF;
  #var beckon OFF;
  #var growth OFF;
  #var rift OFF;
}

#alias enable_panics {
  #send !contingency on;
  #send !paction on;
}

#alias f10 {use_corpse}

#alias f12 {
  #if {$map[current_room] == 22052 || $map[current_room] == 22577} {
    #loop 2 12 i {
      #send !get all from bag $i;
      #send !sacrifice all;
    };
  };
  #else {
    #loop 2 12 i {#send !sell all from bag $i};
  }
}

#alias m {
  #send !contingency off;
  #send !paction off;
  #send !meditate;
}

#alias reof {
  #send !unwrap smallest;
  #send !offer corpse;
}

#alias use_corpse {
  #send !offer corpse;
  #send !hp;
  #send !get all;
}

#alias wimpy_commands {
  #delay 2 {attempt_meditate};
}
