#action {HP: %1/%2 SP: %3/%4 MANA: %5/%6 %7 E: [%8]} {
  #var hp {%1};
  #var hp_max {%2};

  #var sp {%3};
  #var sp_max {%4};

  #var gp1 {%5};
  #var gp1_max {%6};

  #var enemy_str {%8};
  #regexp {$enemy_str} {: %d} {#var enemy &1};

  #if {"$enemy_str" == "none"} {
    #var enemy {0};
    #var enemy_last {0};
  };

  #nop don't need with contingency at glvl 15;
  #nop #if {$sp < 50 && "$enemy_str" != "none"} {!inspirit};
  #if {"$haste" == "off"} {!haste};
}

#action {^GXP%%: [%1%%] PTY: (%2%%) CL: [%3%] PROT: %4 VEST: %5 BARK: %6} {
  #var gxp {%1};
  #var gp2 {%2};
  #var gp2_max {100};
  #var vestment_status {%5};

  #if {"$vestment_status" == "OFF" && "$enemy_str" != "none"} {!vestment};
  #if {"$blade" == "off" && "$enemy_str" != "none"} {!blade};
  show_status_bar;
}

#action {^You finish your meditation and stand up.$} {
  !contingency on;
  !hp;
}

#action {You close your book and stand up, ready to do battle.$} {
  !contingency on;
}

#action {^You break your meditation!$} {
  !contingency on;
}

#action {^The spell casting disrupts your meditation, and the spell fails!$} {
  !contingency on;
}

#action {^%?%sYou do not need to meditate.$} {
  !contingency on;
}

#action {^Your bark-like skin withers away.$} {
  #if {$enemy != 0} {!barkskin};
}

#nop Haste;

#action {^'Wise and powerful %1, hasten my reflexes and quicken my body!'} {
  #var haste {on};
}

#action {^You feel yourself slow back down to normal speed.$} {
  #var {haste} {off};
  #if {$enemy != 0} {!haste};
}

#nop Blade;

#action {^Small razor-sharp blades hiss as they begin to revolve around your body$} {
  #var blade {on};
}

#action {^You are already encircled by a maelstrom of gyrating razor-sharp blades.$} {
  #var blade {on};
}

#action {^The blades stop swirling and shimmer into nothingness.$} {
  #var {blade} {off};
  #if {$enemy != 0} {!blade};
}
