#action {HP: %1/%2 SP: %3/%4 MANA: %5/%6 %7 E: [%8]} {
  #var hp {%1};
  #var hp_max {%2};

  #var sp {%3};
  #var sp_max {%4};

  #var gp1 {%5};
  #var gp1_max {%6};

  #var enemy_str {%8};
  #regexp {$enemy_str} {: %d} {#var enemy &1};

  #if {"$enemy_str" == "none"} {
    #var enemy {0};
    #var enemy_last {0};
  };

  #if {$sp < 50 && "$enemy_str" != "none"} {!inspirit};
  #if {"$haste" == "off"} {!haste};
}

#action {^GXP%%: [%1%%] PTY: (%2%%) PROT: %3 VEST: %4 BARK: %5} {
  #var gxp {%1};
  #var gp2 {%2};
  #var gp2_max {100};
  #var vestment_status {%4};

  #if {"$vestment_status" == "OFF" && "$enemy_str" != "none"} {!vestment};
  show_status_bar;
}

#action {^You finish your meditation and stand up.$} {
  !paction on;
  !hp;
}

#action {You close your book and stand up, ready to do battle.$} {
  !paction on;
}

#action {^You break your meditation!$} {
  !paction on;
}

#action {^Your bark-like skin withers away.$} {
  #if {$enemy != 0} {!barkskin};
}

#action {^'Wise and powerful %1, hasten my reflexes and quicken my body!'} {
  #var haste {on};
}

#action {^You feel yourself slow back down to normal speed.$} {
  #var {haste} {off};
  #if {$enemy != 0} {!haste};
}
