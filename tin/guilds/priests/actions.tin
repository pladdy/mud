#action {HP: %1/%2 SP: %3/%4 MANA: %5/%6 %7 E: [%8]} {
  #var hp {%1};
  #var hp_max {%2};

  #var sp {%3};
  #var sp_max {%4};

  #var gp1 {%5};
  #var gp1_max {%6};

  #var enemy_str {%8};
  #regexp {$enemy_str} {: %d} {#var enemy &1};

  #if {"$enemy_str" == "none"} {#var enemy {0}};
  #replace {enemy} {%} {};

  #nop don't need with contingency at glvl 15;
  #nop #if {$sp < 50 && $enemy > 0} {#2 ins};
}

#action {^GXP%%: [%1%%] PTY: (%2%%) CL: [%3%] PROT: %4 VEST: %5 BARK: %6 HASTE: %7} {
  #var gxp {%1};
  #var gp2 {%2};
  #var gp2_max {100};

  #var protection_status {%4};
  #format {protection_status} {%p} {$protection_status};
  #var vestment_status {%5};
  #format {vestment_status} {%p} {$vestment_status};
  #var barkskin_status {%6};
  #format {barkskin_status} {%p} {$barkskin_status};
  #var haste_status {%7};
  #format {haste_status} {%p} {$haste_status};

  #if {"$barkskin_status" == "OFF" && $enemy > 0} {!barkskin};
  #if {"$vestment_status" == "OFF" && $enemy > 0} {!vestment};
  #if {"$haste_status" == "OFF" && $enemy > 0} {!haste};
  #if {"$blade" == "OFF" && $enemy > 0} {!blade};
  attempt_protection;
  attempt_psummon;
  #nop attempt_pshift;

  show_status_bar;
}

#action {^{(> )*}You finish your meditation and stand up.$} {
  enable_panics;
}

#action {^{(> )*}You close your book and stand up, ready to do battle.$} {
  enable_panics;
}

#action {^{(> )*}You break your meditation!$} {
  enable_panics;
}

#action {^{(> )*}The spell casting disrupts your meditation, and the spell fails!$} {
  enable_panics;
}

#action {^{(> )*}The sudden attack by %1 breaks your meditation!} {
  enable_panics;
}

#action {^{(> )*}You do not need to meditate.$} {
  enable_panics;
}

#nop Haste;

#action {^{(> )*}'Wise and powerful %1, hasten my reflexes and quicken my body!'} {
  #var haste_status {ON};
}

#action {^{(> )*}You feel yourself slow back down to normal speed.$} {
  #if {$enemy > 0} {!haste};
}

#nop Barkskin;

#action {^{(> )*}*Your barkskin has worn off!*$} {
  #if {$enemy > 0} {!barkskin};
}

#action {^{(> )*}Your bark-like skin begins to itch and soften.$} {
  #if {$enemy > 0} {!barkskin};
}

#nop Blade;

#action {^{(> )*}Small razor-sharp blades hiss as they begin to revolve around your body$} {
  #var blade {ON};
}

#action {^{(> )*}You are already encircled by a maelstrom of gyrating razor-sharp blades.$} {
  #var blade {ON};
}

#action {^{(> )*}The blades stop swirling and shimmer into nothingness.$} {
  #var {blade} {OFF};
  #if {$enemy > 0} {!blade};
}

# nop Mends;

#action {^%1 gives you a high-five!} {
  #format {player} {%l} {%1};
  !mend $player;
}

#nop Plane shift;

#action {^{(> )*}Selene, aid my struggle with a gate to the nether planes!$} {
  #var pshift ON;
}

#action {^{(> )*}There is already a plane rift present.$} {
  #var pshift ON;
}

#nop Protection;

#action {^{(> )*}The strands of your protective aura begin to break apart, then fade away.$} {
  attempt_protection;
}

#nop Psummon;

#action {^{(> )*}Suddenly, a cloud elemental arrives from the plane of air to} {
  #var psummon {ON};
}

#action {^{(> )*}You have already summoned help.$} {
  #var psummon {ON};
}

#nop Vestment

#action {^The magical vestment fades into nothingness...$} {
  #if {$enemy > 0} {!vestment};
}
