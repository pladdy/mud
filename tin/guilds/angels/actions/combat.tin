#action {^%!{[> ]*}You consecrate the ground!$} {
  #var consecrate_active true;
}

#action {^%!{[> ]*}Divine energy radiates from the ground!$} {
  #var consecrate_active true;
}

#action {Your consecration in %1 has ended.} {
  #var consecrate_active false;
}

#action {^%!{[> ]*}You divine strike {.+} {high|low} and {inside|outside}.} {
  log.debug Divine strike: %1, %2, %3;

  #local first_letter @first_letter{%2};
  #local second_letter @first_letter{%3};

  #nop a divine strike consumes half the advantage in the area;
  #math {advantages[$first_letter$second_letter]} {
    $advantages[$first_letter$second_letter] / 2
  };

  #var circle {true};
}

#action {^%!{[> ]*}You presage {.+} {high|low} and {inside|outside}.} {
  #local first_letter @first_letter{%2};
  #local second_letter @first_letter{%3};

  #if {"$first_letter" == "h"} {#local opposite_first_letter l};
  #else {#local opposite_first_letter h};

  #nop cut advantage in half and apply +70% to opposite (rough estimate);
  #math {presage_advantage} {$advantages[$first_letter$second_letter] * .75};

  #math {advantages[$first_letter$second_letter]} {
    $advantages[$first_letter$second_letter] * .25
  };
  #math {advantages[$opposite_first_letter$second_letter]} {
    $advantages[$opposite_first_letter$second_letter] + ($presage_advantage * .75)
  };

  sort_advantages;
  #nop try using providence each round;
  #nop attempt_providence;
}

#action {^%!{[> ]*}You are too exhausted!$} {
  #if {$corpses > 0} {reri};
  #else {sound_beeps};
}

#action {^%!{[> ]*}You have no where to retreat!$} {
  #var {can_disengage} {false};
}

#action {^%!{[> ]*}You don't have that many time slices available.$} {
  attempt_circle;
}

#action {^%!{[> ]*}Not enough time slices to %1!$} {
  attempt_circle;
}

#nop Awe emotes to track if a mob is stunned;
#nop awe_length is set to the duration in seconds the stun lasts (info awe);

#action {^%!{[> ]*}Your %1 awe stuns %2!$} {
  #var {awed} {true};
  #delay {$awe_length} {#var {awed} {false};};
}

#action {^%!{[> ]*}You parry %1 attack.$} {
  #var parried true;
  decrement_ts 1;
  #nop  Will traverse only if can't disengage;
  attempt_traverse;
}

#action {^%!{[> ]*}You tuck your wings tight and dive straight at} {
  #var already_dove true;
}

#action {^%!{[> ]*}You have already dove at %1.$} {
  #var already_dove true;
}

#action {^%!{[> ]*}You traverse %1!$} {
  #local max_distance 21;
  #var can_disengage true;
  #if {"%1" == "toward"} {
    #math traverse_distance {$max_distance - $traverse_distance};
    #send !disengage;
  };
}

#action {^%!{[> ]*}You void %1 attack.$} {decrement_ts 1}

#action {^%!{[> ]*}You block %1 attack.$} {decrement_ts 1}

#action {^%!{[> ]*}You fail to block %1 attack.$} {decrement_ts 1}

#action {^%!{[> ]*}You fail to disengage from %1.$} {
  decrement_ts $disengage_distance
}

#action {^%!{[> ]*}You fail to parry %1 attack.$} {decrement_ts 1}

#action {^%!{[> ]*}You fail to reverse %1 attack.$} {decrement_ts 1}

#action {^%!{[> ]*}You fail to void %1 attack!$} {decrement_ts 1}

#action {^%!{[> ]*}% advances!$} {
  #math {angel[distance]} {$angel[distance] - 1};
}

#action {^%!{[> ]*}You retreat!$} {
  decrement_ts $disengage_distance;
  #math {angel[distance]} {$angel[distance] + $disengage_distance};
  #nop attempt_lunge;
}

#action {^%!{[> ]*}You are too close to lunge at %1$} {
  take_swing;
}

#action {^%!{[> ]*}You fail to lunge forward!$} {
  #send !engage $angel[distance];
  take_swing;
}

#action {^%!{[> ]*}%1 doesn't appear that %2 would be awed right now.} {
  #nop try to undo the awe mutation to guild stats;
  #math {gp1} {$gp1 + 2};
  #math {advantages[$highest_area]} {$advantages[$highest_area] * 2};

  #if {@should_retribution{}} {retribution};
  #else {divine_strike};
}