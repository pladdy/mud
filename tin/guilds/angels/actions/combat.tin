#action {^%!{[> ]*}You divine strike {.+} {high|low} and {inside|outside}.} {
  log.debug Divine strike: %1, %2, %3;

  #local first_letter @first_letter{%2};
  #local second_letter @first_letter{%3};

  #nop a divine strike consumes half the advantage in the area;
  #math {advantages[$first_letter$second_letter]} {
    $advantages[$first_letter$second_letter] / 2
  };

  #var circle {true};
}

#nop Not sure why i had this action set up...;
#action {^%!{[> ]*}Your %1 presage at %2 misses.$} {
  #nop attempt_disengage;
};

#action {^%!{[> ]*}You presage {.+} {high|low} and {inside|outside}.} {
  #local first_letter @first_letter{%2};
  #local second_letter @first_letter{%3};

  #if {"$first_letter" == "h"} {#local opposite_first_letter l};
  #else {#local opposite_first_letter h};

  #nop cut advantage in half and apply +70% to opposite (rough estimate);
  #math {presage_advantage} {$advantages[$first_letter$second_letter] * .75};

  #math {advantages[$first_letter$second_letter]} {
    $advantages[$first_letter$second_letter] * .25
  };
  #math {advantages[$opposite_first_letter$second_letter]} {
    $advantages[$opposite_first_letter$second_letter] + ($presage_advantage * .75)
  };

  sort_advantages;
  #nop try using providence each round;
  #nop attempt_providence;
}

#action {^%!{[> ]*}You are too exhausted!$} {
  #if {$corpses > 0} {reri};
  #else {sound_beep};
}

#action {^%!{[> ]*}You have no where to retreat!$} {
  #var {can_disengage} {false};
}

#action {^%!{[> ]*}You don't have that many time slices available.$} {
  attempt_circle;
}

#action {^%!{[> ]*}Not enough time slices to %1!$} {
  attempt_circle;
}

#nop Awe emotes to track if a mob is stunned;
#nop awe_length is set to the duration in seconds the stun lasts (info awe);

#action {^%!{[> ]*}Your %1 awe stuns %2!$} {
  #var {awed} {true};
  #delay {$awe_length} {#var {awed} {false};};
}

#action {^%!{[> ]*}You parry %1 attack.$} {
  #var parried true;
  decrement_ts 1;
  #nop  Will traverse only if can't disengage;
  attempt_traverse;
}

#action {^%!{[> ]*}You tuck your wings tight and dive straight at} {
  #var already_dove true;
}

#action {^%!{[> ]*}You have already dove at %1.$} {
  #var already_dove true;
}

#action {^%!{[> ]*}You traverse %1!$} {#var can_disengage true}

#action {^%!{[> ]*}You void %1 attack.$} {decrement_ts 1}

#action {^%!{[> ]*}You block %1 attack.$} {decrement_ts 1}

#action {^%!{[> ]*}You fail to block %1 attack!$} {decrement_ts 1}

#action {^%!{[> ]*}You fail to parry %1 attack!$} {decrement_ts 1}

#action {^%!{[> ]*}You fail to void %1 attack!$} {decrement_ts 1}