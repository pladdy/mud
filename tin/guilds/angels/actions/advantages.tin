#action {^HO: %1/%2 HI: %3/%4 LO: %5/%6 LI: %7/%8 G: %9} {
  #var gxp {%9};
  #replace {gxp} {%} {};

  show_status_bar;

  #if { @length{%1} > 0 } {
    #var advantages[ho] %1;
    #var advantages[hi] %3;
    #var advantages[lo] %5;
    #var advantages[li] %7;

    #var adv_sort {};
    attempt_swing;
    attempt_presage;
  };
}

#action {^{(> )*}You divine strike {.+} {high|low} and {inside|outside}.} {
  #local first_letter @first_letter{%4};
  #local second_letter @first_letter{%5};

  #nop a divine strike consumes half the advantage in the area;
  #math {advantages[$first_letter$second_letter]} {
    $advantages[$first_letter$second_letter] / 2
  };

  #var circle {true};
}

#action {^{(> )*}Your %1 presage at %2 misses.$} {
  attempt_disengage;
};

#action {^{(> )*}You presage {.+} {high|low} and {inside|outside}.} {
  #local first_letter @first_letter{%4};
  #local second_letter @first_letter{%5};

  #if {"$first_letter" == "h"} {#local opposite_first_letter l};
  #else {#local opposite_first_letter h};

  #nop cut advantage in half and apply +50% to opposite (rough estimate);

  #math {advantages[$first_letter$second_letter]} {
    $advantages[$first_letter$second_letter] / 2
  };
  #math {advantages[$opposite_first_letter$second_letter]} {
    $advantages[$opposite_first_letter$second_letter] * 1.5
  };

  attempt_providence;
}
