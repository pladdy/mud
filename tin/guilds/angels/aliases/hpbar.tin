#alias commands_after_corpse {
  #nop Riposte makes mobs kill themselves...;
  #if {"$last_killer" == "$last_killed"} {
    wrap_or_use_corpse;
    #if {"$flying" == "true"} {#send !swoop all} {#send !get all};
  };

  #var {awed} {false};
  #var {already_dove} {false};
  #var {consecrate_active} {false};
}

#alias f9 {
  #send !wrap corpse;
  #if {"$flying" == "true"} {#send !swoop all} {#send !get all};
}

#alias f10 {
  use_corpse;
  #if {"$flying" == "true"} {#send !swoop all} {#send !get all};
}

#alias min_ts {
  #if {@length{%1} > 0} {
    #showme <170>Setting to %1;
    #var min_ts %1;
  } {
    #showme <170>Current min_ts: $min_ts;
  }
}

#nop Don't use hp in this alias, it messes with angel actions;
#alias redi {
  #send !unwrap;
  #send !disperse;
}

#alias redis {
  #if {"%1" != ""} {
    #loop 1 %1 $loop {redi};
  };
  #else {
    #if {$corpses < 5} {#var max_to_use $corpses} {#var max_to_use 5};
    #if {$corpses > 0} {#loop 1 $max_to_use $loop {redi}};
    #unvar max_to_use;
  };
}

#alias reri {
  #send !unwrap;
  #send !rites;
  #send !disperse;
}

#alias use_corpse {
  #math {gp2_pct_raw} {($gp2 * 1.00) / $gp2_max * 100};
  #math {gp3_pct_raw} {($gp3 * 1.00) / $gp3_max * 100};

  #if {$gp2_pct_raw < $gp3_pct_raw && "$use_disperse" == "true"} {
    #send !disperse;
  } {
    #send !rites;
    #send !disperse;
  };

  #nop consume corpse;
  #if {"$use_spoon" == "true"} {#send !eat corpse};
}
