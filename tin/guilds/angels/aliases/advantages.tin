#alias attempt_disengage {
  #if {$angel[distance] == 0 &&
       "$can_disengage" == "true" &&
       $time_slices > 0} {
    !disengage;
    decrement_ts 1;
  };
}

#alias attempt_presage {
  sort_advantages;
  #if {$angel[distance] == 0 &&
       $adv_sort[1][2] > 20 &&
       $time_slices >= $min_ts} {
    !presage $adv_sort[1][1];
    decrement_ts 1;
  };
  #else {
    attempt_disengage;
  };
}

#alias attempt_providence {
  #if {$angel[distance] == 0 && $time_slices >= $min_ts} {
    sort_advantages;

    #if {$adv_sort[1][2] > $providence_at} {
      !providence $adv_sort[1][1];
      decrement_ts 1;
    };
  };

  attempt_disengage;
}

#alias attempt_swing {
  sort_advantages;

  #if {$angel[distance] == 0 &&
       $adv_sort[1][2] > $swing_at &&
       $time_slices > $min_ts} {
    #math {pct_hp} {$hp / $hp_max * 100.0};
    #local {area} {$adv_sort[1][1]};

    #if {"$awed" != "true" &&
         ("$awe_foes" == "true" || $pct_hp <= $awe_at_hp_pct)} {
      !awe $area;
      decrement_ts 2;
    };
    #elseif {$retributions > $retribution_at} {
      !retribution $area;
      decrement_ts 1;
    };
    #else {
      !divine strike $area;
      decrement_ts 2;
    };

    #nop DS/awe takes half your advantage away and costs 2 TS;
    #math {advantages[$area]} {$advantages[$area] / 2};
    attempt_swing;
  };
}

#alias decrement_ts {
  #math {time_slices} {$time_slices - %1};
}

#alias sort_advantages {
  #nop sort them in descending order by value (k: location, v: advantage);
  #var i {1};
  #list adv_sort clear;

  #forall {ho;hi;lo;li} {
    #if {$i == 1} {
      #var adv_sort[$i] { {1}{&0}{2}{$advantages[&0]} };
    } {
      #var j {1};
      #var found {0};

      #foreach $adv_sort[%*] {item} {
        #if { $advantages[&0] > $adv_sort[$j][2] } {
          #list adv_sort insert $j { {1}{&0}{2}{$advantages[&0]} };
          #var found {1};
          #break;
        };

        #math {j} {$j + 1};
        #replace {j} {.00} {};
      };

      #if {$found == 0} {
        #list adv_sort add $j;
        #list adv_sort insert $j { {1}{&0}{2}{$advantages[&0]} };
        #list adv_sort delete -1;
      };
    };

    #math {i} {$i + 1};
  };
}

