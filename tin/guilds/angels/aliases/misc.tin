#alias auto_train {
  #if {"$auto_train" == "on"} {
    log.info Turning auto train off;
    #var auto_train off;
  };
  #else {
    log.info Turning auto train on;
    #var auto_train on;    
  }
}

#alias check_stepper {
  #local MIN_END 50;

  #if {"$stepper" == "on" && $gp2 < $MIN_END} {
    log.info Creating delay 'reactivate_stepper' to activate stepper after regen.;
    stepper off;
    
    #nop Round is seconds * 2 (2s / round);
    #local min_seconds 60;
    #math rando {1 d 10};
    #math regen_time {$min_seconds + $rando};

    #delay {reactivate_stepper} {
      log.info "Re-activating stepper after regen time: $regen_time seconds.";
      stepper on;
    } {$regen_time};

    #unvar rando;
  };
  #else {
    #format {current_time} {%T};
    #math {time_since_stepper_started} {$current_time - $stepper_started};
  
    #if {"$stepper" == "on" && $time_since_stepper_started > 750} {
      stepper off;
  
      log.info Creating delay 'reactivate_stepper' to activate stepper after regen.;
  
      #nop Round is seconds * 2 (2s / round);
      #local min_seconds 60;
      #math rando {1 d 20};
      #math regen_time {$min_seconds + $rando};
  
      #delay {reactivate_stepper} {
        log.info "Re-activating stepper after regen time: $regen_time seconds.";
        stepper on;
      } {$regen_time};
  
      #unvar rando;
    };
  };
}

#alias f12 {
  #if {$map[current_room] == 220} {#loop 2 12 i {#send !junk all from bag $i}};
  #else {#loop 2 12 i {#send !sell all from bag $i}}
}

#alias party_tank {
  #var use_traverse true;
  #var use_disengage true;
}

#alias party_hit {
  #var use_traverse false;
  #var use_disengage false;
}

#nop Based on input, perform scales on items or against a mob;
#alias sca {
  #if {@length{%2} > 0} {#send !scales %0;} {#send !scales against %1;};
}

#alias scales_bot {
  #if {"%1" == "on"} {
    #showme <170>Activating scales bot.;
    #var scales_bot on;
    #4 scales against $character;

    #showme <170>Using scales bot again in 20 seconds.;
    #delay {20} {
      #if {"$scales_bot" == "on"} {scales_bot on}
    };
  } {
    #showme <170>Deactivating scales bot.;
    #var scales_bot off;
  }
}

#alias show_trainings {
  #list angel_train size angel_trainings;

  #if {$angel_trainings > 0} {
    #foreach { $angel_train[%*] } {skill} {
      sound_beep;
      #if { @length{$skill} > 0 } { #showme <fca>Go Train $skill; };
    };
    #unvar skill;
  };

  #unvar angel_trainings;
}

#alias train {
  #if {"$auto_train" == "on"} {
    #if {"$stepper" == "on"} {
      #var reactivate_stepper true;
      stepper off;
    };

    #var current_room $map[current_room];
    #map run angels_train;
    #send !train %0;
    #unvar angel_train;

    #map run $current_room;
    #unvar current_room;

    #delay 4 {
      #if {"$reactivate_stepper" == "true"} {
        stepper on;
        #unvar reactivate_stepper;
      };
    };
  };
  #else {
    #send !train %0;
  }
}
