#action {^%!{[> ]*}%!{(?:C:)?}HP[%1/%2] SP[%3/%4] NP[%5/%6|%7c] E[%8]} {
  log.debug hpbar match;

  #nop  Handle hpfuse;
  #regexp {%1} {%d(%d)} {#var hp &1; #var hpfuse &2} {#var hp %1; #var hpfuse 0};
  #var hp_max {%2};

  #nop  Handle spfuse;
  #regexp {%3} {%d(%d)} {#var sp &1; #var spfuse &2} {#var sp %3; #var spfuse 0};
  #var sp_max {%4};

  #var gp1 {%5};
  #var gp1_max {%6};

  #var ncorpses %7;

  #math hpp {($hp * 1.00) / ($hp_max * 1.00)};
  #math spp {($sp * 1.00) / ($sp_max * 1.00)};
  #math gp1p {($gp1 * 1.00) / ($gp1_max * 1.00)};

  #var enemy {%8};
  #if {"$enemy" == "none"} {#var enemy {0}};
  #regexp {%8} {%+:%d%} {#var enemy &2};

  attempt_dart;
  attempt_call;
  attempt_spfuse;
  attempt_dream;
  attempt_glamor;
  attempt_preserve;
  check_stepper;
}

#alias _status_aliases {
  show_status_bar;
  attempt_protection;
  attempt_refill_corpses;
  attempt_unload_corpses;
  attempt_follower_heal;
}

#alias _status_vars {
  log.debug status vars is called with %0;

  #var teleports %1;
  #var glamors_str %2;

  log.debug glamors is %2;

  #regexp {$glamors_str} {{(\d+)/\d+%}} {
    #var glamors &2;
    #var glamor_active true;
  } {
    #var glamors $glamors_str;
    #var glamor_active false;
  };

  #var gp2 {%3};
  #var gp2_max 100;

  #var worth %3;
  #var protection %4;
  #var veil %5;
  #var repower %6;
  #format {necro_follower} {%l} {%7}  
}

#action {^%!{[> ]*}TG[%1|%2] Status[w%3%|p%4|v%5|r%6%] %7[%8%/%9c/%10c]} {
  log.debug status bar match with follower and with coffin;
  _status_vars %1 %2 %3 %4 %5 %6 %7;
  #var necro_follower_health %8;
  #var follower_corpses %9;
  #var corpses %10;

  _status_aliases;
}

#action {^%!{[> ]*}TG[%1|%2] Status[w%3%|p%4|v%5|r%6%] %7[%8%/%9c]} {
  log.debug status bar match with follower and without coffin;
  _status_vars %1 %2 %3 %4 %5 %6 %7;
  #var necro_follower_health %8;
  #var follower_corpses %9;

  _status_aliases;
}

#action {^%!{[> ]*}TG[%1|%2] Status[w%3%|p%4|v%5|r%6%] %7[{none|unraised}/%8c]} {
  log.debug status bar match without follower and with coffin;
  log.debug 7 is %7 and 8 is %8;
  _status_vars %1 %2 %3 %4 %5 %6 %7;
  #var corpses %8;
  #var follower_corpses 0;

  _status_aliases;
}

#action {^%!{[> ]*}TG[%1|%2] Status[w%3%|p%4|v%5|r%6%] %7[{none|unraised}]} {
  log.debug status bar match without follower and without coffin;
  _status_vars %1 %2 %3 %4 %5 %6 %7;
  #var corpses 0;
  #var follower_corpses 0;
  _status_aliases;
}

#nop  Teleport to RR room;

#action {^%!{[> ]*}Visions of the guild appear first in your mind, then reality...$} {
  #map goto 26792;
}

#action {^%!{[> ]*}%1 the %2 moves into the battle to help} {
  #var familiar_called true;
}

#action {^%!{[> ]*}Mojo the %1 can't carry that much more.$} {
  #send !keep corpse;
}

#nop  Coffin in inventory;

#action {^%!{[> ]*}Coffin%s[%s%1/%s%2|%3%|%s%4c] %5} {
  #var corpses %1;
  #if {%1 != %2} {
    #var coffin_status not_full;
  }
}

#nop  Dart actions;

#action {^%!{[> ]*}You don't have create dart memorized this cycle.$} {
  #var can_dart false;
}

#action {^%!{[> ]*}Master, you have used your last create dart.$} {
  #var can_dart false;
}

#action {^%!{[> ]*}%1 tells you: You have completed %1 studies of create dart.$} {
  #var can_dart true;
}

#action {create dart(2): %1  %2} {
  #var can_dart true;
}

#nop  Dream actions;

#action {^%!{[> ]*}  You don't have dream memorized this cycle.$} {
  #var can_dream false;
}

#action {^%!{[> ]*}Master, you have used your last dream.$} {
  #var can_dream false;
}

#action {^%!{[> ]*}%1 tells you: You have completed %1 studies of dream.$} {
  #var can_dream true;
}

#action {dream(3): %1  %2} {
  #var can_dream true;
}

#action {^%!{[> ]*}Your body relaxes from the strain.$} {
  #var glamor_active false;
}

#action {^%!{[> ]*}You begin to generate magical energy!$} {
  #var glamor_active true;
}

#action {^%!{[> ]*}You are already sparking.$} {
  #var glamor_active true;
}

#nop  Follower actions;

#action {^%!{[> ]*}Your %1 finds its way around the magical barrier.$} {
  #var follower_blocked false;
}

#action {^%!{[> ]*}Some magic blocks your %1 from following you here.$} {
  #var follower_blocked true;
}

#action {^%!{[> ]*}Your %1 is not here.$} {
  #if {"%1" == "$necro_follower"} {
    #var follower_blocked true;
  };
}

#action {^%!{[> ]*}A %1 trudges in.$} {
  #format {trudger} {%l} {%1};
  #if {"$trudger" == "$necro_follower"} {
    #var follower_blocked false;
  };
}

#action {^%!{[> ]*}Mojo the %1 trudges in.$} {
  #format {trudger} {%l} {%1};
  #if {"$trudger" == "$necro_follower"} {
    #var follower_blocked false;
  };
}

#nop for some reason text won't match with '^';
#action {%!{[> ]*}Your %1 begins to follow you.$} {
  #showme %1 is here;

  #if {"%1" == "$necro_follower"} {
    #var follower_blocked false;
  };
}

#nop  Reagents;

#action {^ |%sginseng:%1%sblack pearls:%2%sspider web:%3%sgoldenrod:%4%s|$} {
  log.debug Reagents line 1.;
  #var ginseng %1;
  #var black_pearls %2;
  #var spider_web %3;
  #var goldenrod %4;
}

#action {^ |%smandrake:%1%spine needles:%2%snightshade:%3%sbloodmoss:%4%s|$} {
  log.debug Reagents line 2.;
  #var mandrake %1;
  #var pine_needles %2;
  #var nightshade %3;
  #var bloodmoss %4;
}

#nop  Smuggling;

#action {^Smuggling%s[%s%1/%s%2|%3%|%s%4c]} {
  #var smuggle[count] %1;
  #var smuggle[max] %2;
  #var smuggle[corpses] %4;
}

#action {^You smuggle away: ~(%1) a preserved, %2 corpse of %3$} {
  #math smuggle[corpses] {$smuggle[corpses] + 1};
  #math smuggle[count] {$smuggle[count] + 1};
}

#action {^You unsmuggle: ~?(%1) a preserved, %2 corpse of %3$} {
  #math smuggle[corpses] {$smuggle[corpses] - 1};
  #math smuggle[count] {$smuggle[count] - 1};
}

#action {^%!{[> ]*}%1 taps you on the shoulder.$} {
  #5 {
    #send !preserve corpse;
    #send !get corpse;
  };
}

#action {^%!{[> ]*} You don't have dream memorized this cycle.} {
  #if {"$stepper" == "on"} {stepper off};
}