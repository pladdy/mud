#action {^%!{[> ]*}%WHP: %1/%2 PP: %3/%4 Chi: %5/%6 G2:%7 Mind: %8 AE:%9/%10% CF:%11$} {
  #var hp {%1};
  #var hp_max {%2};

  #var sp {%3};
  #var sp_max {%4};

  #var gp1 %5;
  #var gp1_max %6;

  #var gxp %7;
  #var gp2 {%8};

  #var ancient_energy %9;
  #var reset %10;

  #local MIN_MIND 170;
  #if {$gp2 < $MIN_MIND} {#showme <110>Mind is low!};

  #if {"$stepper" == "on" && $gp2 < $MIN_MIND} {
    log.warn Turnning stepper off.  Mind: $gp2, Stepper: $stepper;
    stepper off;
    dtrig f2;
  };

  #regexp {%11} {T: %d%?$} {
    #var enemy &1;
    #math {rounds_in_combat} {$rounds_in_combat + 1};
  } {
    #var enemy 0;
    #var rounds_in_combat 0;
  };

  #local MIN_SP 100;
  #if {$sp < $MIN_SP && $corpses > 0 && $enemy > 0} {resp};

  attempt_enervate;
  attempt_focus;
  attempt_palm;
  attempt_pressure;
  attempt_throw;
  show_status_bar;
}

#nop Briefed hpbar;

#action {^%!{[> ]*}%WH:%1/%2 P:%3/%4 C:%5/%6 G2:%7 M:%8 AE:%9/%10% CF:%11$} {
  #var hp {%1};
  #var hp_max {%2};

  #var sp {%3};
  #var sp_max {%4};

  #var gp1 %5;
  #var gp1_max %6;

  #var gxp %7;
  #var gp2 {%8};

  #var ancient_energy %9;

  #switch {"$gp2"} {
    #case "seld" { #var gp2 {180}; };
    #case "clea" { #var gp2 {200}; };
    #case "focu" { #var gp2 {220}; };
    #case "unwa" { #var gp2 {240}; };
    #case "indo" { #var gp2 {280}; };
    #default { #var gp2 {0}; };
  };

  #regexp {%11} {T: %d%?$} {#var enemy &1} {#var enemy 0};

  attempt_enervate;
  attempt_focus;
  attempt_palm;
  attempt_pressure;
  attempt_throw;
  show_status_bar;
}

#action {^%!{[> ]*}You feel your body and mind relax a little.$} {
  #send !hp;
}

#action {^%!{[> ]*}You need not meditate further, type 'stopmed' to stop.$} {
  #if {"$keep_meditating" == "true"} {
    log.info Keep Meditating set to true.;
    #send !hp;
  };
  #elseif {$hp >= $hp_max && $sp >= $sp_max} {
    #send !stopmed;
  };
  #else {
    #send !hp;
  }
}
