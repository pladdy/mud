#action {^%!{[> ]*}H: %1/%2 S: %3/%4 W: %5/%6(%7) L: %8% P: %9(%10%) E#: %11 E: %12 $} {
  #var hp {%1};
  #var hp_max {%2};

  #var sp {%3};
  #var sp_max {%4};

  #var gp1 {%5};
  #var gp1_max {%6};

  #var suit_water {%7};
  #math {total_water} {$gp1 + $suit_water};
  #format total_water_pct {%f} {$total_water / $gp1_max};

  #var enemy {%12};
  #if {"$enemy" == "none"} {#var enemy 0};
  #replace {enemy} {%} {};

  #if {$total_water_pct < 1.3 && $gp1 < 50 && $corpses > 0} {rerc};
  show_status_bar;
}

#action {^%!{[> ]*}Li: %1 Sp: %2 Pa: %3 Ma: %4(%5) %6 Gt:%7 G:} {
  #var gxp %7;

  #local spice_level %2;
  #list spice_queue size available_spices;
  #local ammo %4;
  #local ammo_max %5;

  #if {$enemy > 0 && "$ammo" != "--"} {
    #if {$ammo == 1 && $ammo_max > 0} {#send !mload};
  };

  #if {$enemy > 0 && "$spice_level" == "Withdrawal" && $available_spices > 0} {
    #list spice_queue get 1 spice_task;
    !$spice_task;
    #list spice_queue delete 1;
  };
}

#action {^%!{[> ]*}Again, the trip is short, the hatch opens and you step out onto the$} {
  #if {$map[last_room] == 8164} {
    #map goto 63;
  } {
    #map goto 8164;
  }
}

#nop Spice purchases and inventory;

#action {^%!{[> ]*}# Item$} {
  #list spice_queue clear;
}

#action {^%!{[> ]*}You pay %1 gold coins for %*tea.$} {
  #list spice_queue add {drink tea};
}

#action {^%!{[> ]*}You pay %1 gold coins for %*coffee.$} {
  #list spice_queue add {drink coffee};
}

#action {^%!{[> ]*}You pay %1 gold coins for %*beer.$} {
  #list spice_queue add {drink beer};
}

#action {^%!{[> ]*}You pay %1 gold coins for %*liquor.$} {
  #list spice_queue add {drink liquor};
}

#action {^%!{[> ]*}You pay %1 gold coins for pure spice.$} {
  #list spice_queue add {take spice};
}

#action {^%!{[> ]*}%1 %!{[\*]?}Spice Tea.$} {
  #%1 #list spice_queue add {drink tea};
}

#action {^%!{[> ]*}%1 %!{[\*]?}Spice Coffee.$} {
  #%1 #list spice_queue add {drink coffee};
}

#action {^%!{[> ]*}%1 %!{[\*]?}Spice Beer.$} {
  #%1 #list spice_queue add {drink beer};
}

#action {^%!{[> ]*}%1 %!{[\*]?}Spice Liquor.$} {
  #%1 #list spice_queue add {drink liquor};
}

#action {^%!{[> ]*}%1 %!{[\*]?}Spice Melange (Pure).$} {
  #%1 #list spice_queue add {take spice};
}
