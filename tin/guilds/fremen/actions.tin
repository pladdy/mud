#action {^%!{[> ]*}H: %1/%2 S: %3/%4 W: %5/%6(%7) L: %8% P: %9(%10%) E#: %11 E: %12 $} {
  #var hp {%1};
  #var hp_max {%2};

  #var sp {%3};
  #var sp_max {%4};

  #var gp1 {%5};
  #var gp1_max {%6};

  #var suit_water {%7};
  #math {total_water} {$gp1 + $suit_water};
  #format total_water_pct {%f} {$total_water / $gp1_max};

  #var enemy {%12};
  #if {"$enemy" == "none"} {#var enemy 0};
  #replace {enemy} {%} {};

  #if {$total_water_pct < 1.3 && $gp1 < 50 && $corpses > 0} {rerc};
  attempt_stun;

  show_status_bar;
}

#action {^%!{[> ]*}Li: %1 Sp: %2 Pa: %3 Ma: %4(%5) %6 Gt:%7 G:} {
  #var gxp %7;

  #local spice_level %2;
  #local ammo %4;
  #local ammo_max %5;

  #if {$enemy > 0 && "$ammo" != "--"} {
    #if {$ammo == 1 && $ammo_max > 0} {#send !mload};
  };

  count_available_spices;
  #if {$enemy > 0 && "$spice_level" == "Withdrawal" && $available_spices > 0} {
    respice;
  };
}

#action {^%!{[> ]*}%1 recoils from your voice and stops attacking!$} {
  #var voice_active true;
}

#action {^%!{[> ]*}%1 recovers from the Voice!$} {
  #delay {voice_delay} {#var voice_active false;} {100};
}

#action {^%!{[> ]*}That creature will not be affected.$} {
  #var voice_active true;
}

#action {^%!{[> ]*}%1 cannot be stunned again so quickly!$} {
  #var stun_active true;
}

#action {^%!{[> ]*}You pull out your stunner and fire it at %1.$} {
  #var stun_active true;
}

#action {^%!{[> ]*}%1 shakes off the effects of the stunner.$} {
  #delay {stun_delay} {#var stun_active false;} {100};
}

#action {^%!{[> ]*}Again, the trip is short, the hatch opens and you step out onto the$} {
  #if {$map[last_room] == 8164} {
    #map goto 63;
  } {
    #map goto 8164;
  }
}

#nop Spice purchases and inventory;

#action {^%!{[> ]*}# Item$} {
  #list spice_inv_queue clear;
}

#action {^%!{[> ]*}You pay %1 gold coins for %*tea.$} {
  #list spice_inv_queue add {drink tea};
}

#action {^%!{[> ]*}You pay %1 gold coins for %*coffee.$} {
  #list spice_inv_queue add {drink coffee};
}

#action {^%!{[> ]*}You pay %1 gold coins for %*beer.$} {
  #list spice_inv_queue add {drink beer};
}

#action {^%!{[> ]*}You pay %1 gold coins for %*liquor.$} {
  #list spice_inv_queue add {drink liquor};
}

#action {^%!{[> ]*}You pay %1 gold coins for pure spice.$} {
  #list spice_inv_queue add {take spice};
}

#nop  Spices in inventory;

#action {^%!{[> ]*}{\d+} %!{[\*]?}Spice {[A-Za-z]+}.$} {
  #format {drink} {%l} {%2};
  #%1 #list spice_inv_queue add {drink $drink};
}

#action {^%!{[> ]*}{\d+} %!{[\*]?}Spice Melange (Pure).$} {
  #%1 #list spice_inv_queue add {take spice};
}

#nop  Spices in bags;

#action {^Spice {[A-Za-z]+}\.$} {
  #format {drink} {%l} {%1};
  #list spice_bag_queue add {drink $drink};
}

#action {^Spice {[A-Za-z]+} \{{\d+}\}\.$} {
  #format {drink} {%l} {%1};
  #%2 #list spice_bag_queue add {drink $drink};
}

#action {^Spice Melange (Pure)\.$} {
  #list spice_bag_queue add {take spice};
}

#action {^Spice Melange (Pure) \{{\d+}\}\.$} {
  #%2 #list spice_bag_queue add {take spice};
}
