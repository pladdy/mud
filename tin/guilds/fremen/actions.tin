#nop second line of hpbar required to c
#action {^%!{[> ]*}Li: %1 Sp: %2 Pa: %3 Ma: %4(%5) %6 C: %7 Gt: %8 G:} {
  #var litany_focus %1;

  #var spice_level %2;
  #local ammo %4;
  #local ammo_max %5;
  #var corpses %7;

  #if {$rounds_in_combat > 0 && "$ammo" != "--"} {
    #if {$ammo == 1 && $ammo_max > 0} {#send !mload};
  };

  count_available_spices;
  #if {$rounds_in_combat > 0 
       && ("$spice_level" == "Withdrawal" || "$spice_level" == "None")
       && $available_spices > 0} {
    respice;
  };
}

#nop  Voice;

#action {^%!{[> ]*}%1 recoils from your Voice and stops attacking!$} {
  #var voice_active true;
}

#action {^%!{[> ]*}%1 recoils from your voice} {
  #var voice_active true;
}

#action {^%!{[> ]*}%1 recovers from the Voice!$} {
  #var voice_recovering true;
  #var voice_active false;
  #delay {voice_delay} {#var voice_recovering false;} {20};
}

#action {^%!{[> ]*}That creature will not be affected.$} {
  #var voice_active true;
}

#action {^%!{[> ]*}Your Voice falters as you notice that %1 is unaffected.$} {
  #math voice_failures {$voice_failures + 1};
}

#action {^%!{[> ]*}The Golem calls for help!} {
  #send !voice golem 1 to peace;
}

#nop  Stun;

#action {^%!{[> ]*}%1 cannot be stunned again so quickly!$} {
  #var stun_active true;
  #delay {stun_delay} {#var stun_active false;} {5};
}

#action {^%!{[> ]*}You pull out your stunner and fire it at %1.$} {
  #var stun_active true;
}

#action {^%!{[> ]*}%1 shakes off the effects of the stunner.$} {
  #var stun_recovering true;
  #var stun_active false;
  #delay {stun_delay} {#var stun_recovering false;} {20};
}

#action {^%!{[> ]*}Unaffected and enraged, %1 turns to you and attacks!$} {
  #math stun_failures {$stun_failures + 1};
  #var stun_active false;
}

#nop  Fremen guild;

#action {^%!{[> ]*}Again, the trip is short, the hatch opens and you step out onto the$} {
  #if {$map[last_room] == 8164} {
    #map goto 63;
  } {
    #map goto 8164;
  }
}

#nop  Spice;

#action {^%!{[> ]*}# Item$} {
  #list spices clear;
  #send !look at bag;
}

#nop  Spice purchases;

#action {^%!{[> ]*}You pay %1 gold coins for {[A-Za-z]+}.$} {
  #format {drink} {%l} {%2};
  #list spices[inv] add {drink $drink};
}

#action {^%!{[> ]*}You pay %1 gold coins for pure spice.$} {
  #list spices[inv] add {take spice};
}

#nop  Spices in inventory;

#action {^%!{[> ]*}{\d+} %!{[\*]?}Spice {[A-Za-z]+}.$} {
  #format {drink} {%l} {%2};
  #%1 #list spices[inv] add {drink $drink};
}

#action {^%!{[> ]*}{\d+} %!{[\*]?}Spice Melange (Pure).$} {
  #%1 #list spices[inv] add {take spice};
}

#nop  Spices in bags;

#action {^%!{[> ]*}Spice {[A-Za-z]+}: Ok.$} {
  #format {drink} {%l} {%1};
  #list spices[bag] delete 1;
  #list spices[inv] add {drink $drink};
}

#action {^%!{[> ]*}You put Spice {[A-Za-z]+} in %2.$} {
  #format {drink} {%l} {%1};
  #list spices[bag] add {drink $drink};
  #list spices[inv] delete 1;
}

#action {^%!{(\[o\] )?}Spice {[A-Za-z]+}\.$} {
  #format {drink} {%l} {%2};
  #list spices[bag] add {drink $drink};
}

#action {^%!{(\[o\] )?}Spice {[A-Za-z]+} \{{\d+}\}\.$} {
  #format {drink} {%l} {%2};
  #%3 #list spices[bag] add {drink $drink};
}

#action {^Spice Melange (Pure)\.$} {
  #list spices[bag] add {take spice};
}

#action {^Spice Melange (Pure) \{{\d+}\}\.$} {
  #%2 #list spices[bag] add {take spice};
}

#action {^%!{[> ]*}%1 cannot be stunned!$} {
  #var stun_active true;
  #send !voice;
}

#nop Hide;

#action {You are already hidden.} {
  #var can_hide false;
}

#action {You cannot seem to use your hide skills here.} {
  #var can_hide false;
}