#alias attempt_mental_depletion {
  #local MIN_SP_PCT 0.5;
  #math {sp_as_pct} {($sp * 1.00)/ $sp_max * 100};

  #if {"$mind_depleted" != "true" && $sp_as_pct < $MIN_SP_PCT && $enemy > 0} {
    #send !evoke mental depletion;
  };
}

#alias attempt_trap_soul {
  #math {damage_left} {$enemy};
  #math {damage_so_far} {100.0 - $enemy};

  #if {$damage_left > 0} {
    #math {average_dmg} {$damage_so_far / $rounds_in_combat};
    #math {rounds_left} {$damage_left / $average_dmg};
    #if {$rounds_left < 2.0 &&
         $gp1 > 75 &&
         "$trap_souls" == "yes" &&
         "$trapping_soul" != "true"} {
      #send !ts;
      #nop try to store gem in vortex, keep gem in case it can't be stored;
      btrig put gem in vortex;
    };
  };
}

#alias cbl {
  #send !evoke create blast;
  #send !keep blast;
}

#alias commands_before_corpse {
  #var trapping_soul false;
  #var mind_depleted false;
}

#alias gbl {
  #if {"%0" != ""} {
    #send !unkeep blast;
    #send !activate blast;
    #send !give blast to %0;
  }
}

#alias f10 {
  use_corpse;
  #send !get all;
}

#alias f12 {
  #loop 2 12 i {#send !sell all from bag $i};
  #nop #send !sell all from vortex;
}

#alias md {
  #send !evoke mental depletion;
}

#alias regen {
  run el_apex;
}

#alias recd {
  #send !unwrap smallest;
  use_corpse;
}

#alias reet {
  #send !unwrap smallest;
  #send !eat corpse;;
}

#alias rg {
  #send !get gem from vortex;
  #send !release soul;
}

#alias trap_souls {
  #if {"$trap_souls" != "yes"} {
    #var trap_souls yes;
    #showme <170>Trapping souls enabled.;
  } {
    #var trap_souls no;
    #showme <170>Trapping souls disabled.;
  }
}

#alias use_corpse {
  #send !evoke corpse drain;
}
