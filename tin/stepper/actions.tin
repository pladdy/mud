#nop Track monsters when stepping through rooms;

#nop Note: Mud events for a mob dying are defined in tin/default/kills.tin;
#nop       removing_seen_monster and activating the TTL are handled in those;
#nop       events;

#nop Room Short trigger;

#action {^%!{[> ]*}[RS] %1} {
  #list seen_monsters clear;
  #var seen_monster_count 0;
  #var under_attack false;
  #var player_present false;
  #var tracker on;
  #var tried_to_auto_kill false;

  parse_exits %1;
}

#nop Monster trigger;

#action {^%!{[> ]*}[M] %1} {
  log.debug monster trigger firing;

  set_under_attack %1;
  #local mob_on_blacklist {@mob_is_blacklisted{%1}};

  log.debug mob_on_blacklist? $mob_on_blacklist;

  #if {"$mob_on_blacklist" == "false"} {
    #script {monster_name} {./bin/monster_name "%1"};

    #local monster_name $monster_name[1];
    #format {monster_name} {%l} {$monster_name};

    #list {seen_monsters} {find} {$monster_name} {index_if_seen};

    #if {$index_if_seen == 0} {
      #list {seen_monsters} {add} {$monster_name};
    };
  };
}

#nop Room Prompt;

#action {^{(> )+}} {
  #if {"$tried_to_auto_kill" == "false"} {
    #if {"$stepper" == "on"} {
      auto_kill;
      #var tried_to_auto_kill true;
    };
  };
}

#action {^[P] %1} {
  log.debug Tracker found player present, player %1;
  #var player_present {true};
}

#action {^[{A|O|o|W}] %1} {
   log.debug Tracker found item present, item %1;
   #var item_present {true};
}

#action {^There is no %1 here.} {
  #regexp {%1} {reason to}
    {}
    {
      #list {seen_monsters} {find} {%1} {mob_index};

      #if {$mob_index > 0} {
        #list {seen_monsters} {delete} {$mob_index};
      };

      #send !glance;
  };
}
