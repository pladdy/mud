#nop Track monsters when stepping through rooms;

#nop Room Short trigger;

#action {^[RS] %1$} {
  #list seen_monsters clear;
  #var under_attack false;
  #var tracker on;
}

#nop Monster trigger;

#action {^[M] %1.} {
  under_attack %1;
  #script {monster_name} {./bin/monster_name "%1"};
  #var monster_name $monster_name[1];
  #format {monster_name} {%l} {$monster_name};

  #list {seen_monsters} {find} {$monster_name} {index_if_seen};
  #if {$index_if_seen == 0} {
    #list {seen_monsters} {add} {$monster_name};
  };
  #unvar monster_name;
}

#nop Monster deaths;

#action {^%1 gasps for breath, then slumps into death.$} {
  remove_seen_monster %1;
}

#action {^%1 screeches in agony and falls to the ground, dead.$} {
  remove_seen_monster %1;
}

#action {^%1 gurgles in %2 own blood as %3 dies.$} {
  remove_seen_monster %1;
}

#nop If a mob is no longer present, remove from list;

#action {There is no %1 here.} {
  #regexp {%1} {reason to} {} {
    #list {seen_monsters} {find} {%1} {mob_index};
    #if {$mob_index > 0} {
      #list {seen_monsters} {delete} {$mob_index};
    };
  }
}

#alias ak {auto_kill}

#alias auto_kill {
  #list {seen_monsters} {size} {seen_monster_count};
  #if {$seen_monster_count > 0 & $under_attack == "false"} {
    !kill $seen_monsters[1];
  };
}

#nop Given a monster, try to remove from seen_monster list;

#alias remove_seen_monster {
  #format {mob_to_remove} {%l} {%0};
  #list {seen_monsters} {find} {$mob_to_remove} {mob_index};

  #if {$mob_index > 0} {
    #list {seen_monsters} {delete} {$mob_index};
  };
  #unvar mob_to_remove;
  #unvar mob_index;
}

#nop Simple wrapper alias to enable/disable tracking of mobs/players;

#alias tracker {
  #if {"%0" == "on"} {
    #showme <170>Enabling tracking. 'tracker off' to disable.;
    #var tracker on;
    !aset room_short white+[RS] +reset;
    !aset look_monster hired+[M] +reset;
    !aset look_player higreen+[P] +reset;
    !brief on no;
  } {
    #showme <170>Disabling tracking. 'tracker on' to re-enable.;
    #var tracker off;
    !aset room_short;
    !aset look_monster;
    !aset look_player;
    !brief on yes;
  }
}

#nop Set variable to note if under attack or not;

#alias under_attack {
  #regexp {%0} { attacking you!} {#var under_attack true} {#var under_attack false};
}
