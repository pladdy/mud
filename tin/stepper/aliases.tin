#nop build a map of valid dirs;
#var valid_stepper_dirs[n] {valid};
#var valid_stepper_dirs[s] {valid};
#var valid_stepper_dirs[e] {valid};
#var valid_stepper_dirs[w] {valid};
#var valid_stepper_dirs[nw] {valid};
#var valid_stepper_dirs[ne] {valid};
#var valid_stepper_dirs[sw] {valid};
#var valid_stepper_dirs[se] {valid};

#alias _reset_prompt_ticker {
  #unticker prompt;
  #ticker {prompt} {
    #if {$rounds_in_combat == 0} {
      auto_kill;
    };
  } {2}
}

#alias _transform_monster_name {
  log.debug Trying to parse mosnter name %0;

  #var monster_name %0;
  #replace monster_name {[M]} {};
  #replace monster_name {\\x7B%d\\x7D} {};
  #replace monster_name {\\x7b%d\\x7d} {};
  #replace monster_name {attacking you!} {};
  #replace monster_name {[{.+}]} {};
  #replace monster_name {({.+})} {};
  #replace monster_name {<{.+}>} {};
  #replace monster_name {.$} {};
  #format monster_name {%l} $monster_name;
  #format monster_name {%p} $monster_name;
  log.debug Parsed monster name is $monster_name;
}

#alias auto_kill {
  _reset_prompt_ticker;
  #list {seen_monsters} {size} {seen_monster_count};

  #if {"$stepper" == "on" && "$player_present" == "true"} {
    log.debug player present;
    take_a_step;
  };
  #elseif {"$stepper" == "on" && $seen_monster_count == 0 && "$player_present" == "false"} {
    log.debug no monsters present;
    #if {"$item_present" == "true"} {
      #if {"$flying" == "true"} {#send !swoop all};
      #else {#send !get all};
      pba;
    };
    take_a_step;
  };
  #elseif {$seen_monster_count > 0 && "$under_attack" == "false" && "$player_present" == "false"} {
    log.debug monster present, attack;
    #send !kill @transform_mob{$seen_monsters[1]};
    #var under_attack true;
  };

  #if {"$under_attack" == "true"} {ttl_no_monsters};
}

#alias ak {auto_kill}

#alias before_step_hook {#nop;}

#alias keep_cardinal_dirs {
  #list cardinal_exits clear;

  #foreach {$available_exits} {exit} {
    #if {"$valid_stepper_dirs[$exit]" == "valid"} {
      #list cardinal_exits add $exit;
    };
  }
}

#alias load_non_monsters {
  #script {non_monsters} {cat tin/stepper/non_monsters.txt | grep -v '#'};
  log.debug non_monsters: $non_monsters;
}

#alias parse_exits {
  log.debug parse_exists called with %0;
  #var available_exits {};
  #var room_string %0;
  #var exit_str "";

  #regexp {$room_string} {(%+)$} {#var exit_str &1};

  #if {"$exit_str" != ""} {
    #replace {exit_str} {,} {;};
  };
  #var available_exits $exit_str;

  #unvar room_string;
  #unvar exit_str;
}

#alias remove_seen_monster {
  #format {mob_to_remove} {%l} {%0};

  #foreach *seen_monsters[] {index} {
    #regexp {$seen_monsters[$index]} {$mob_to_remove} {
      #list seen_monsters {delete} {$index};
    };
  };
}

#alias set_entered_from {
  #switch {"$step_dir"} {
    #case {"n"} {#var entered_from s};
    #case {"s"} {#var entered_from n};
    #case {"w"} {#var entered_from e};
    #case {"e"} {#var entered_from w};
    #case {"nw"} {#var entered_from se};
    #case {"ne"} {#var entered_from sw};
    #case {"sw"} {#var entered_from ne};
    #case {"se"} {#var entered_from nw};
    #default {#unvar entered_from};
  }
}

#alias set_under_attack {
  #regexp {%0} { attacking you!} {#var under_attack true};
  #regexp {%0} { [%d%] attacking} {#var under_attack true};
}

#alias stepper {
  #if {"%0" == "on"} {
    #showme <170>Enabling stepping. 'stepper off' to disable.;
    #format {stepper_started} {%T};
    #unvar step_dir;
    #unvar entered_from;
    #delay {2} {
      #var stepper on;
      #send !glance;
    };
    tracker on;
    ttl_stepper on;
    ttl_no_monsters on;
    #event {RECEIVED INPUT} {
      log.info Input received.  Refreshing stepper TTL.;
      ttl_stepper on;
    };
    #ticker {prompt} {
      #if {$rounds_in_combat == 0} {#send !glance};
    } {2};
} {
    #showme <170>Disabling stepping. 'stepper on' to re-enable.;
    tracker off;
    #var stepper off;
    #unvar seen_monster_count;
    ttl_stepper;
    ttl_no_monsters;
    #unevent {RECEIVED INPUT};
    #undelay reactivate_stepper;
    #delay {2} {#unticker prompt};
  }
}

#alias take_a_step {
  before_step_hook;
  keep_cardinal_dirs;
  set_entered_from;
  
  #list cardinal_exits find $entered_from entered_from_index;

  #if {$entered_from_index > 0} {
    #list cardinal_exits delete $entered_from_index;
  };

  #list cardinal_exits size cardinal_exit_count;
  #var dir_to_go @choose_a_dir{$cardinal_exits};

  #if {$cardinal_exit_count > 0} {
    #var step_dir $dir_to_go;
    log.info Stepping in direction $dir_to_go;
    $dir_to_go;
  } {
    #var step_dir $entered_from;
    $entered_from;
  };

  #unvar entered_from_index;
}

#alias tracker {
  #if {"%0" == "on"} {
    #showme <170>Enabling tracking. 'tracker off' to disable.;

    #send !aset room_short white+[RS] +reset;
    #send !aset look_monster hired+[M] +reset;
    #send !aset look_player higreen+[P] +reset;
    #send !aset look_armor hicyan+[A] +reset;
    #send !aset look_object hicyan+[O] +reset;
    #send !aset look_other hicyan+[o] +reset;
    #send !aset look_weapon hicyan+[W] +reset;

    #send !brief on no;
    load_non_monsters;
    #var tracker on;
  } {
    #showme <170>Disabling tracking. 'tracker on' to re-enable.;
    #send !aset room_short;
    #send !aset look_monster;
    #send !aset look_player;
    #send !aset look_armor;
    #send !aset look_object;
    #send !aset look_other;
    #send !aset look_weapon;
    #send !brief on yes;
    unload_non_monsters;
    #var tracker off;
  }
}


#alias ttl_no_monsters {
  #local TTL 60;
  #local ticker_name ttl_no_monsters;

  #if {"%0" == "on"} {
    #undelay $ticker_name;
    #ticker {$ticker_name} {stepper off;ttl_stepper_hook} {$TTL};
  };
  #else {
    #undelay $ticker_name;
  };
}

#alias ttl_stepper {
  #nop  15 minutes;
  #local TTL 900;
  #local ticker_name ttl_stepper;

  #if {"%0" == "on"} {
    log.info Activating stepper TTL for $TTL seconds.;

    #undelay $ticker_name;
    #ticker {$ticker_name} {
      log.warn No input received in a while.  Disabling stepper.;
      stepper off;
      ttl_stepper_hook;
    } {$TTL};
  };
  #else {
    #undelay $ticker_name;
  };
}

#alias ttl_stepper_hook {dtrig #delay 2 f2}

#alias unload_non_monsters {
  #unvar non_monsters;
}
